//! virtio: the two transports, the virtqueue, and the block device.
//!
//! **THIS IS THE DEVICE THE FLOOR'S DISK DOOR WAS ALREADY SHAPED FOR.** A
//! virtio-blk request is a header naming a sector, a data buffer named by
//! ADDRESS, and a status byte -- which is `Disk.read!(lba, addr)` and its
//! outcome, arrived at from the other direction. Nothing here copies a sector;
//! the device writes into the page the caller named.
//!
//! **TWO TRANSPORTS, ONE DRIVER.** On QEMU's `microvm` (and Firecracker) a
//! device is a window of registers at a known address, with a magic value at
//! offset 0 and no bus to enumerate: virtio-mmio. On a PC, and so on a
//! DigitalOcean droplet, the same devices sit on the PCI bus and say, through
//! their capability list, which of their memory windows holds which controls:
//! virtio-pci. Everything above "write the status", "pick a queue" and "ring
//! the doorbell" is the same, so a `Device` is either kind and the rest of
//! this file does not ask which.
//!
//! The spec is virtio 1.2 (§4.1 PCI, §4.2 MMIO). Register offsets and
//! constants below are its numbers, not ours. Only the modern interface is
//! spoken on either transport.

const std = @import("std");
const props = @import("coverage");
const tsc = @import("tsc.zig");
const pci = @import("pci.zig");
const scsi = @import("scsi.zig");
const durable = @import("durable.zig");

/// A device, found on whichever transport this machine has.
pub const Device = union(enum) {
    /// The base of its register window.
    mmio: usize,
    pci: Pci,

    pub fn describe(self: Device) []const u8 {
        return switch (self) {
            .mmio => "virtio-mmio",
            .pci => "virtio-pci",
        };
    }

    /// Where it is, for a report: the register window on mmio, the common
    /// configuration's window on pci.
    pub fn address(self: Device) u64 {
        return switch (self) {
            .mmio => |base| base,
            .pci => |p| p.common,
        };
    }
};

/// Where a PCI device's four kinds of control live, from its capabilities.
pub const Pci = struct {
    function: pci.Function,
    common: usize,
    notify: usize,
    notify_multiplier: u32,
    isr: usize,
    device: usize,
    /// MSI-X table entry 0, once `prepareMsix` has turned MSI-X on. From then
    /// the device never uses the ISR for its queues (virtio §4.1.4.5).
    msix_entry: ?usize = null,
};

// ---- the mmio register window --------------------------------------------

const Reg = enum(u32) {
    magic = 0x000, // "virt", 0x74726976
    version = 0x004, // 2 for the modern interface, 1 for legacy
    device_id = 0x008, // 2 is block, 1 is net
    vendor_id = 0x00c,
    device_features = 0x010,
    device_features_sel = 0x014,
    driver_features = 0x020,
    driver_features_sel = 0x024,
    queue_sel = 0x030,
    queue_num_max = 0x034,
    queue_num = 0x038,
    queue_ready = 0x044,
    queue_notify = 0x050,
    interrupt_status = 0x060,
    interrupt_ack = 0x064,
    status = 0x070,
    queue_desc_lo = 0x080,
    queue_desc_hi = 0x084,
    queue_driver_lo = 0x090,
    queue_driver_hi = 0x094,
    queue_device_lo = 0x0a0,
    queue_device_hi = 0x0a4,
    config = 0x100,
};

const magic_value: u32 = 0x74726976;

// ---- the pci common configuration (§4.1.4.3) -----------------------------
//
// **EACH FIELD IS READ AND WRITTEN AT ITS OWN WIDTH.** The spec requires it,
// and the widths are not all 32 bits: the status is a byte, a queue's size a
// 16-bit word.

const common_device_feature_select = 0x00; // u32
const common_device_feature = 0x04; // u32
const common_driver_feature_select = 0x08; // u32
const common_driver_feature = 0x0C; // u32
const common_device_status = 0x14; // u8
const common_queue_select = 0x16; // u16
const common_queue_size = 0x18; // u16: the device's maximum, until we write ours
const common_queue_msix_vector = 0x1A; // u16
const common_queue_enable = 0x1C; // u16
const common_queue_notify_off = 0x1E; // u16
const common_queue_desc = 0x20; // u64
const common_queue_driver = 0x28; // u64
const common_queue_device = 0x30; // u64

/// The capability types a virtio-pci device lists (§4.1.4).
const cap_common: u8 = 1;
const cap_notify: u8 = 2;
const cap_isr: u8 = 3;
const cap_device: u8 = 4;

/// Status bits the driver walks up in order; the device watches them.
const status_acknowledge: u32 = 1;
const status_driver: u32 = 2;
const status_driver_ok: u32 = 4;
const status_features_ok: u32 = 8;
const status_failed: u32 = 128;

/// VIRTIO_F_VERSION_1: the driver speaks the modern interface. Without it a
/// modern device refuses to start.
const feature_version_1: u6 = 32;

pub const device_id_block: u32 = 2;
pub const device_id_net: u32 = 1;

/// The device may read the rings the instant the index moves, so the stores
/// that build a chain have to land before the store that publishes it.
fn fence() void {
    asm volatile ("mfence" ::: .{ .memory = true });
}

fn read8(at: usize) u8 {
    return @as(*volatile u8, @ptrFromInt(at)).*;
}
fn read16(at: usize) u16 {
    return @as(*volatile u16, @ptrFromInt(at)).*;
}
fn read32(at: usize) u32 {
    return @as(*volatile u32, @ptrFromInt(at)).*;
}
fn write8(at: usize, v: u8) void {
    @as(*volatile u8, @ptrFromInt(at)).* = v;
}
fn write16(at: usize, v: u16) void {
    @as(*volatile u16, @ptrFromInt(at)).* = v;
}
fn write32(at: usize, v: u32) void {
    @as(*volatile u32, @ptrFromInt(at)).* = v;
}
fn write64(at: usize, v: u64) void {
    write32(at, @truncate(v));
    write32(at + 4, @truncate(v >> 32));
}

fn mmioRead(base: usize, reg: Reg) u32 {
    return read32(base + @intFromEnum(reg));
}

fn mmioWrite(base: usize, reg: Reg, value: u32) void {
    write32(base + @intFromEnum(reg), value);
}

// ---- the operations, once per transport ----------------------------------

fn setStatus(d: Device, v: u32) void {
    switch (d) {
        .mmio => |base| mmioWrite(base, .status, v),
        .pci => |p| write8(p.common + common_device_status, @truncate(v)),
    }
}

fn getStatus(d: Device) u32 {
    return switch (d) {
        .mmio => |base| mmioRead(base, .status),
        .pci => |p| read8(p.common + common_device_status),
    };
}

fn deviceFeatures(d: Device, select: u32) u32 {
    switch (d) {
        .mmio => |base| {
            mmioWrite(base, .device_features_sel, select);
            return mmioRead(base, .device_features);
        },
        .pci => |p| {
            write32(p.common + common_device_feature_select, select);
            return read32(p.common + common_device_feature);
        },
    }
}

fn driverFeatures(d: Device, select: u32, value: u32) void {
    switch (d) {
        .mmio => |base| {
            mmioWrite(base, .driver_features_sel, select);
            mmioWrite(base, .driver_features, value);
        },
        .pci => |p| {
            write32(p.common + common_driver_feature_select, select);
            write32(p.common + common_driver_feature, value);
        },
    }
}

/// The device-specific configuration: a block device's capacity, a network
/// card's address.
fn configAddress(d: Device, off: u32) usize {
    return switch (d) {
        .mmio => |base| base + @intFromEnum(Reg.config) + off,
        .pci => |p| p.device + off,
    };
}

pub fn configRead8(d: Device, off: u32) u8 {
    return read8(configAddress(d, off));
}

pub fn configRead16(d: Device, off: u32) u16 {
    return read16(configAddress(d, off));
}

pub fn configRead32(d: Device, off: u32) u32 {
    return read32(configAddress(d, off));
}

fn configRead64(d: Device, off: u32) u64 {
    const at = configAddress(d, off);
    return (@as(u64, read32(at + 4)) << 32) | read32(at);
}

/// Every window QEMU's microvm machine puts a virtio-mmio transport in: slots
/// of 512 bytes from 0xFEB00000.
///
/// **THERE ARE 24 OF THEM AND QEMU FILLS THEM FROM THE TOP.** A single
/// `-device virtio-blk-device` lands on bus 23, at 0xFEB02E00, with the first
/// eight slots present-but-empty -- which looks exactly like "no device" if
/// you only scan eight. Measured with `info qtree`, 2026-09-16. 32 is scanned
/// for headroom; an empty transport answers device id 0 and costs one read.
pub const mmio_base: usize = 0xFEB00000;
pub const mmio_stride: usize = 0x200;
pub const mmio_slots: usize = 32;

/// What a slot says it is, for a host that wants to report the scan rather
/// than only its verdict.
pub fn magicAt(base: usize) u32 {
    return mmioRead(base, .magic);
}
pub fn versionAt(base: usize) u32 {
    return mmioRead(base, .version);
}
pub fn deviceIdAt(base: usize) u32 {
    return mmioRead(base, .device_id);
}

/// The first device of this kind, or null.
pub fn find(want: u32) ?Device {
    return findNth(want, 0);
}

/// The `n`th device of this kind (0 is the first), or null. **A MACHINE WITH
/// A PCI BUS IS ASKED THROUGH IT**, and only a machine without one has its
/// mmio slots scanned: the two are never mixed, so a PCI window that happened
/// to sit where an mmio slot would cannot be mistaken for one. A droplet has
/// two network cards (public, then private, in slot order) and two disks (the
/// boot disk, then the config drive), which is what `n` is for.
pub fn findNth(want: u32, n: usize) ?Device {
    var seen: usize = 0;
    if (pci.present()) {
        var scan = pci.Scan{};
        while (scan.next()) |f| {
            if (pciType(f) != want) continue;
            const d = pciDevice(f) orelse continue;
            if (seen == n) return .{ .pci = d };
            seen += 1;
        }
        return null;
    }
    var i: usize = 0;
    while (i < mmio_slots) : (i += 1) {
        const base = mmio_base + i * mmio_stride;
        // A slot whose magic is wrong is empty; one whose version is not 2 is
        // the legacy interface, which this driver does not speak.
        if (mmioRead(base, .magic) != magic_value) continue;
        if (mmioRead(base, .device_id) != want) continue;
        if (mmioRead(base, .version) != 2) continue;
        if (seen == n) return .{ .mmio = base };
        seen += 1;
    }
    return null;
}

/// The virtio device type of a PCI function, or 0 if it is not virtio.
/// **TWO NUMBERINGS** (§4.1.2.1): a modern-only device is 0x1040 plus its
/// type; a transitional one (what a droplet has: 0x1000 for its network cards,
/// 0x1001 for its disks) is 0x1000-0x103F and says its type in the subsystem
/// id instead.
fn pciType(f: pci.Function) u32 {
    if (f.vendor() != 0x1AF4) return 0;
    const id = f.device();
    if (id >= 0x1040 and id <= 0x107F) return id - 0x1040;
    if (id >= 0x1000 and id <= 0x103F) return f.subsystem();
    return 0;
}

/// Reads the capability list into the four windows, and lets the device at
/// memory. Null if any of the four is missing: that is a legacy-only device,
/// which this driver does not speak.
fn pciDevice(f: pci.Function) ?Pci {
    var common: ?usize = null;
    var notify: ?usize = null;
    var isr: ?usize = null;
    var device: ?usize = null;
    var multiplier: u32 = 0;
    var caps = f.capabilities();
    while (caps.nextOne()) |cap| {
        if (cap.id != 0x09) continue; // vendor-specific: virtio's
        const kind = f.read8(cap.at + 3);
        const bar = f.bar(f.read8(cap.at + 4)) orelse continue;
        const at: usize = @intCast(bar + f.read32(cap.at + 8));
        switch (kind) {
            cap_common => common = common orelse at,
            cap_notify => if (notify == null) {
                notify = at;
                multiplier = f.read32(cap.at + 16);
            },
            cap_isr => isr = isr orelse at,
            cap_device => device = device orelse at,
            else => {},
        }
    }
    const found = Pci{
        .function = f,
        .common = common orelse return null,
        .notify = notify orelse return null,
        .notify_multiplier = multiplier,
        .isr = isr orelse return null,
        .device = device orelse return null,
    };
    f.enable();
    return found;
}

// ---- the virtqueue -------------------------------------------------------

pub const Desc = extern struct {
    addr: u64,
    len: u32,
    flags: u16,
    next: u16,
};

pub const desc_flag_next: u16 = 1;
/// The DEVICE writes this buffer; without it the device reads.
pub const desc_flag_write: u16 = 2;

/// In the used ring's flags, set by the device: it is already working
/// through the queue and needs no doorbell (VIRTQ_USED_F_NO_NOTIFY).
pub const used_flag_no_notify: u16 = 1;
/// In the available ring's flags, set by us: we will look for completions
/// ourselves and want no interrupt for them (VIRTQ_AVAIL_F_NO_INTERRUPT).
/// A hint the device may ignore.
pub const avail_flag_no_interrupt: u16 = 1;

/// A virtqueue's three rings, laid out as one block of memory the caller owns.
/// The device is told where each ring is and then reads and writes them
/// directly: no ports, no copying, one doorbell.
///
/// Sized by its job. A block queue needs one chain in flight; a receive queue
/// wants as many buffers as it can hold frames.
pub fn Ring(comptime size: u16) type {
    return extern struct {
        const Self = @This();
        pub const len: u16 = size;

        desc: [size]Desc align(16),

        avail_flags: u16 align(2),
        avail_idx: u16,
        avail_ring: [size]u16,
        avail_used_event: u16,

        used_flags: u16 align(4),
        used_idx: u16,
        used_ring: [size]UsedElem,
        used_avail_event: u16,
    };
}

pub const UsedElem = extern struct { id: u32, len: u32 };

/// One queue on a device: the ring, which queue index it is, and how far we
/// have read its used ring.
pub fn Queue(comptime size: u16) type {
    return struct {
        const Self = @This();
        pub const RingType = Ring(size);

        device: Device,
        index: u16,
        /// The device took MSI-X entry 0 as this queue's vector, before the
        /// queue was enabled.
        vectored: bool = false,
        ring: *RingType,
        last_used: u16 = 0,
        /// Where this queue's doorbell is: one register for every queue on
        /// mmio, a register of its own on pci.
        doorbell: usize,

        /// Tells the device where this queue's rings are and marks it ready.
        /// Must happen before DRIVER_OK.
        pub fn setup(device: Device, index: u16, ring: *RingType) Error!Self {
            const ring_addr = @intFromPtr(ring);
            const avail_addr = ring_addr + @offsetOf(RingType, "avail_flags");
            const used_addr = ring_addr + @offsetOf(RingType, "used_flags");
            var doorbell: usize = undefined;
            var vectored = false;
            switch (device) {
                .mmio => |base| {
                    mmioWrite(base, .queue_sel, index);
                    if (mmioRead(base, .queue_num_max) < size) return Error.QueueTooSmall;
                    mmioWrite(base, .queue_num, size);
                    mmioWrite(base, .queue_desc_lo, @truncate(ring_addr));
                    mmioWrite(base, .queue_desc_hi, @truncate(ring_addr >> 32));
                    mmioWrite(base, .queue_driver_lo, @truncate(avail_addr));
                    mmioWrite(base, .queue_driver_hi, @truncate(avail_addr >> 32));
                    mmioWrite(base, .queue_device_lo, @truncate(used_addr));
                    mmioWrite(base, .queue_device_hi, @truncate(used_addr >> 32));
                    mmioWrite(base, .queue_ready, 1);
                    doorbell = base + @intFromEnum(Reg.queue_notify);
                },
                .pci => |p| {
                    write16(p.common + common_queue_select, index);
                    // Zero is "no such queue"; otherwise the device's maximum.
                    const max = read16(p.common + common_queue_size);
                    if (max < size) return Error.QueueTooSmall;
                    write16(p.common + common_queue_size, size);
                    write64(p.common + common_queue_desc, ring_addr);
                    write64(p.common + common_queue_driver, avail_addr);
                    write64(p.common + common_queue_device, used_addr);
                    const off = read16(p.common + common_queue_notify_off);
                    doorbell = p.notify + @as(usize, off) * p.notify_multiplier;
                    // **THE VECTOR BEFORE THE QUEUE IS ENABLED** (virtio
                    // §4.1.5.1.3), as Linux sets it: a device may read it only
                    // then. Entry 0 is still masked; `routeToProcessor` aims
                    // and unmasks it later. A device that would not take it
                    // reads back NO_VECTOR, and the queue goes on polled.
                    if (p.msix_entry != null) {
                        write16(p.common + common_queue_msix_vector, 0);
                        vectored = read16(p.common + common_queue_msix_vector) == 0;
                    }
                    write16(p.common + common_queue_enable, 1);
                },
            }

            ring.avail_flags = 0;
            ring.avail_idx = 0;
            ring.used_idx = 0;
            return .{ .device = device, .index = index, .vectored = vectored, .ring = ring, .doorbell = doorbell };
        }

        /// Puts the chain starting at descriptor `head` on the available ring.
        /// The caller has already filled the descriptors.
        pub fn offer(self: *Self, head: u16) void {
            self.ring.avail_ring[self.ring.avail_idx % size] = head;
            fence();
            self.ring.avail_idx +%= 1;
            fence();
        }

        /// **A DOORBELL ONLY WHEN THE DEVICE WANTS ONE** (virtio §2.7.7). Each
        /// ring of it is a trip out to the hypervisor; a device already
        /// working through the queue says so in the used ring's flags, read
        /// after `offer` has published the new entries.
        pub fn notifyIfWanted(self: *Self) void {
            fence();
            const flags = @as(*volatile u16, @ptrCast(&self.ring.used_flags)).*;
            if (flags & used_flag_no_notify == 0) self.notify();
        }

        /// Whether the device interrupts when it completes something here.
        pub fn interruptOnCompletion(self: *Self, on: bool) void {
            @as(*volatile u16, @ptrCast(&self.ring.avail_flags)).* = if (on) 0 else avail_flag_no_interrupt;
            fence();
        }

        /// Rings the doorbell for this queue.
        pub fn notify(self: *Self) void {
            switch (self.device) {
                .mmio => write32(self.doorbell, self.index),
                .pci => write16(self.doorbell, self.index),
            }
        }

        /// The next completion, or null if the device has published none.
        pub fn take(self: *Self) ?UsedElem {
            const idx = @as(*volatile u16, @ptrCast(&self.ring.used_idx)).*;
            if (idx == self.last_used) return null;
            const e = self.ring.used_ring[self.last_used % size];
            self.last_used +%= 1;
            return e;
        }

        /// Spins until a completion arrives. Nothing here has interrupts yet.
        pub fn wait(self: *Self) UsedElem {
            while (true) {
                if (self.take()) |e| return e;
                asm volatile ("pause");
            }
        }
    };
}

/// Walks the device up to DRIVER_OK, taking VIRTIO_F_VERSION_1 and whatever
/// else the caller names in `want_low` (feature bits below 32). Every step is
/// checked, because a device that has silently refused looks exactly like one
/// that is working until the first transfer hangs.
///
/// Answers the status word so far; the caller adds its queues, then calls
/// `driverOk`.
pub fn negotiate(d: Device, want_low: u32) Error!u32 {
    setStatus(d, 0); // reset
    // **A RESET IS NOT DONE UNTIL THE DEVICE SAYS SO** on pci (§4.1.4.3.1):
    // the status reads back 0 once it is. mmio's reset is immediate.
    if (d == .pci) {
        while (getStatus(d) != 0) asm volatile ("pause");
    }
    var st: u32 = status_acknowledge;
    setStatus(d, st);
    st |= status_driver;
    setStatus(d, st);

    const hi = deviceFeatures(d, 1);
    if (hi & (@as(u32, 1) << (feature_version_1 - 32)) == 0) {
        setStatus(d, status_failed);
        return Error.DeviceRefused;
    }
    const lo = deviceFeatures(d, 0);
    if (lo & want_low != want_low) {
        setStatus(d, status_failed);
        return Error.DeviceRefused;
    }

    driverFeatures(d, 1, @as(u32, 1) << (feature_version_1 - 32));
    driverFeatures(d, 0, want_low);

    st |= status_features_ok;
    setStatus(d, st);
    if (getStatus(d) & status_features_ok == 0) {
        setStatus(d, status_failed);
        return Error.DeviceRefused;
    }
    return st;
}

pub fn driverOk(d: Device, st: u32) Error!void {
    setStatus(d, st | status_driver_ok);
    if (getStatus(d) & status_failed != 0) return Error.DeviceRefused;
}

/// The interrupt this device raised, acknowledged. Polling drivers still have
/// to do this or the device stops raising them. On pci, reading the ISR byte
/// is the acknowledgement — unless MSI-X is on, when the ISR says nothing
/// about the queues and the read would only be a trip to the hypervisor per
/// frame.
pub fn ack(d: Device) void {
    switch (d) {
        .mmio => |base| {
            const s = mmioRead(base, .interrupt_status);
            if (s != 0) mmioWrite(base, .interrupt_ack, s);
        },
        .pci => |p| if (p.msix_entry == null) {
            _ = read8(p.isr);
        },
    }
}

/// **MSI-X TURNED ON, WITH NOTHING SENT YET.** On PCI a device raises an
/// interrupt by writing a message to an address — MSI-X — and the table of
/// messages it may write sits in one of its memory windows, named by its
/// MSI-X capability (PCI 3.0 §6.8.2). Entry 0 is masked first, then MSI-X is
/// enabled for the function: from here the device sends nothing (a masked
/// entry only sets its pending bit) until `routeToProcessor` aims and
/// unmasks it. Called after `negotiate`'s reset and before `Queue.setup`, so
/// that each queue's vector is set before the queue is enabled. Answers the
/// device unchanged on mmio, or when it has no MSI-X capability.
pub fn prepareMsix(d: Device) Device {
    var p = switch (d) {
        .pci => |p| p,
        .mmio => return d,
    };
    const f = p.function;
    var caps = f.capabilities();
    const cap = while (caps.nextOne()) |c| {
        if (c.id == 0x11) break c;
    } else return d;
    const table = f.read32(cap.at + 4);
    const window = f.bar(@truncate(table & 7)) orelse return d;
    const entry: usize = @intCast(window + (table & ~@as(u32, 7)));
    write32(entry + 12, 1); // masked
    write32(entry + 0, 0);
    write32(entry + 4, 0);
    write32(entry + 8, 0);

    // Enable (bit 15), and not masked as a whole (bit 14).
    const control = f.read16(cap.at + 2);
    f.write16(cap.at + 2, (control | 0x8000) & ~@as(u16, 0x4000));
    p.msix_entry = entry;
    return .{ .pci = p };
}

/// **A DEVICE THAT INTERRUPTS THE PROCESSOR WHEN IT HAS SOMETHING.** MSI-X
/// entry 0, which `prepareMsix` turned on masked and `Queue.setup` gave the
/// queues, is aimed at `address` with `vector` and unmasked. A message the
/// device wanted to send while it was masked is sent now. False, and nothing
/// changed, on mmio or on a device `prepareMsix` found no MSI-X on.
pub fn routeToProcessor(d: Device, address: u32, vector: u8) bool {
    const entry = switch (d) {
        .pci => |p| p.msix_entry orelse return false,
        .mmio => return false,
    };
    write32(entry + 0, address);
    write32(entry + 4, 0);
    write32(entry + 8, vector);
    write32(entry + 12, 0); // unmasked
    return true;
}

pub const Error = error{ NoDevice, DeviceRefused, QueueTooSmall, TransferFailed };

// ---- the block device ----------------------------------------------------

const blk_t_in: u32 = 0; // read from the device
const blk_t_out: u32 = 1; // write to the device

/// The three status bytes virtio-blk answers with. **The floor's Disk door
/// invented an outcome; this is where the outcome comes from.**
pub const blk_s_ok: u8 = 0;
pub const blk_s_ioerr: u8 = 1;
pub const blk_s_unsupp: u8 = 2;

const BlkReqHeader = extern struct {
    type: u32,
    reserved: u32,
    sector: u64,
};

pub const Block = struct {
    pub const Q = Queue(8);

    device: Device,
    q: Q,
    header: *BlkReqHeader,
    status: *volatile u8,
    /// The device's size in 512-byte sectors, from its config space.
    capacity: u64,

    /// **WHAT THE DEVICE COST.** Every request counted, and the timestamp-counter
    /// ticks spent between offering it and seeing it done — so a host can say
    /// how much of a slow HTTP request was the disk, rather than guessing. The
    /// soak could only see the whole machine slowing down; these split it.
    requests: u64 = 0,
    busy_ticks: u64 = 0,

    /// **ON A SCSI CONTROLLER, THE DISK THIS BLOCK IS** (`scsi.zig`), and the
    /// memory its commands are built in. Null on virtio-blk.
    address: ?scsi.Address = null,
    scsi: ?*scsi.Memory = null,

    /// **WRITES THE DISK HAS ANSWERED BUT MAY NOT HAVE KEPT.** A disk with a
    /// write cache answers a write once the bytes are in the cache; only a
    /// flush makes them durable. Set by every write, cleared by `flush`.
    /// `io.durable` reads it before any response leaves the machine.
    unflushed: bool = false,
    /// What the disk says of its own cache: true (writes wait in it), false
    /// (it writes through), or null (it did not say). A SCSI disk says in
    /// MODE SENSE's caching page (`scsi.zig`). A virtio-blk disk is
    /// write-through unless VIRTIO_BLK_F_FLUSH is negotiated (virtio 1.2
    /// §5.2.5.1), and this driver never negotiates it, so it is false there.
    write_cache: ?bool = null,
    /// Flushes sent, and those the disk answered with an error.
    flushes: u64 = 0,
    flush_failures: u64 = 0,

    /// **A DISK IN MEMORY, FOR HOST TESTS** (`inMemory`): every transfer is a
    /// copy to or from these bytes, and no device is touched. Null on a real
    /// device.
    memory: ?[]u8 = null,
    /// A host test's way to make the disk fail: once this many requests have
    /// been served, every further one answers `blk_s_ioerr`.
    fail_after: ?u64 = null,
    /// The same, counting only writes: once this many have been served,
    /// every further request fails. A machine stopped after its Nth write
    /// (QUEUE.md item 79); the reads between two writes change nothing.
    fail_after_writes: ?u64 = null,
    /// Writes served, on a disk in memory.
    writes: u64 = 0,
    /// A host test's lie, told once, at request number `at` (counted as
    /// `requests` counts) on a disk in memory (QUEUE.md item 80).
    fault: ?Fault = null,

    pub const Fault = struct {
        at: u64,
        kind: enum {
            /// The request answers `blk_s_ioerr`, and the next one is served.
            fails,
            /// A write answers OK and nothing lands.
            lands_nothing,
            /// A write of more than one sector answers OK and only its first
            /// half lands: a torn write.
            torn,
            /// A read answers OK with other bytes than the disk's: what a
            /// short read leaves in the buffer, or a device that lies.
            garbage,
        },
        /// The bytes `garbage` reads.
        seed: u8 = 0xA5,
    };

    /// A disk of `bytes.len / 512` sectors held in `bytes`, which the caller
    /// owns. For host tests of what sits on a disk (`fat16.zig`, `io.zig`):
    /// on a host every address is the caller's own memory, so a transfer is a
    /// copy. Nothing here may be called on it but the transfers.
    pub fn inMemory(bytes: []u8) Block {
        return .{
            .device = .{ .mmio = 0 },
            .q = undefined,
            .header = undefined,
            .status = undefined,
            .capacity = bytes.len / 512,
            .memory = bytes,
        };
    }

    fn memoryTransfer(self: *Block, disk: []u8, kind: u32, lba: u64, addr: u64, len: u32) u8 {
        if (self.fail_after) |n| if (self.requests >= n) return blk_s_ioerr;
        if (self.fail_after_writes) |n| if (self.writes >= n) return blk_s_ioerr;
        const number = self.requests;
        self.requests +%= 1;
        if (kind != blk_t_in) {
            self.writes +%= 1;
            self.unflushed = true;
        }
        const at = lba * 512;
        if (at > disk.len or len > disk.len - at) return blk_s_ioerr;
        const there = disk[@intCast(at)..][0..len];
        const here: [*]u8 = @ptrFromInt(@as(usize, @intCast(addr)));
        if (self.fault) |f| if (f.at == number) {
            switch (f.kind) {
                .fails => return blk_s_ioerr,
                .lands_nothing => if (kind != blk_t_in) return blk_s_ok,
                .torn => if (kind != blk_t_in and len > 512) {
                    const half = len / 512 / 2 * 512;
                    @memcpy(there[0..half], here[0..half]);
                    return blk_s_ok;
                },
                .garbage => if (kind == blk_t_in) {
                    for (here[0..len], 0..) |*b, i| b.* = @truncate(i *% 167 +% f.seed);
                    return blk_s_ok;
                },
            }
        };
        if (kind == blk_t_in) @memcpy(here[0..len], there) else @memcpy(there, here[0..len]);
        return blk_s_ok;
    }

    /// `mem` is memory the caller owns and keeps for as long as the device is
    /// up; it must be identity-mapped, since what goes in a descriptor is a
    /// PHYSICAL address.
    pub fn init(device: Device, mem: *BlockMemory) Error!Block {
        const st = try negotiate(device, 0);
        const q = try Q.setup(device, 0, &mem.ring);
        try driverOk(device, st);
        return .{
            .device = device,
            .q = q,
            .header = &mem.header,
            .status = &mem.status,
            .capacity = configRead64(device, 0),
            // No features negotiated, so no VIRTIO_BLK_F_FLUSH: write-through.
            .write_cache = false,
        };
    }

    /// One request, start to finish, polled to completion. The chain is the
    /// spec's three descriptors: the header the device reads, the data buffer,
    /// and the status byte the device writes.
    ///
    /// **`addr` IS A PHYSICAL ADDRESS AND THE DEVICE WRITES IT DIRECTLY.**
    /// That is the whole reason the door takes an address: the 512 bytes never
    /// pass through this function.
    fn transfer(self: *Block, kind: u32, lba: u64, addr: u64, len: u32) u8 {
        if (self.memory) |disk| return self.memoryTransfer(disk, kind, lba, addr, len);
        if (self.address) |at| {
            // Set before the command: a write that fails may still have
            // landed in part, in the cache.
            if (kind != blk_t_in) self.unflushed = true;
            return scsi.transfer(self, at, kind == blk_t_in, lba, addr, len);
        }
        self.header.* = .{ .type = kind, .reserved = 0, .sector = lba };
        self.status.* = 0xFF; // so a device that writes nothing is not read as OK

        const d = &self.q.ring.desc;
        d[0] = .{ .addr = @intFromPtr(self.header), .len = @sizeOf(BlkReqHeader), .flags = desc_flag_next, .next = 1 };
        d[1] = .{
            .addr = addr,
            .len = len,
            .flags = desc_flag_next | (if (kind == blk_t_in) desc_flag_write else 0),
            .next = 2,
        };
        d[2] = .{ .addr = @intFromPtr(self.status), .len = 1, .flags = desc_flag_write, .next = 0 };

        const began = tsc.read();
        self.q.offer(0);
        self.q.notify();
        _ = self.q.wait();
        ack(self.device);
        self.busy_ticks +%= tsc.read() -% began;
        self.requests +%= 1;
        return self.status.*;
    }

    /// The sector at `lba` into the 512 bytes at `addr`.
    pub fn read(self: *Block, lba: u64, addr: u64) u8 {
        return self.transfer(blk_t_in, lba, addr, 512);
    }

    /// The most sectors one request asks for. virtio-blk lets a device state
    /// its own limit only through features this driver does not negotiate, so
    /// the number is ours: 64 KB, well inside what any device accepts in a
    /// single segment, and enough that a file's run of clusters is one request
    /// rather than one per sector.
    pub const max_sectors: u32 = 128;

    /// `count` sectors from `lba` into the `count` × 512 bytes at `addr`, as ONE
    /// request. **This is what makes reading a file cost a request per run of
    /// clusters rather than a request per 512 bytes** — the soak read a 200 KB
    /// transcript on every chat send, and at a sector per request that was four
    /// hundred round trips through the emulator.
    pub fn readMany(self: *Block, lba: u64, addr: u64, count: u32) u8 {
        if (count == 0 or count > max_sectors) return blk_s_unsupp;
        return self.transfer(blk_t_in, lba, addr, count * 512);
    }

    /// The 512 bytes at `addr` become the sector at `lba`.
    pub fn write(self: *Block, lba: u64, addr: u64) u8 {
        return self.transfer(blk_t_out, lba, addr, 512);
    }

    /// The `count` × 512 bytes at `addr` become `count` sectors from `lba`, as
    /// ONE request — what makes a 10 MB upload about 160 requests instead of
    /// twenty thousand.
    pub fn writeMany(self: *Block, lba: u64, addr: u64, count: u32) u8 {
        if (count == 0 or count > max_sectors) return blk_s_unsupp;
        return self.transfer(blk_t_out, lba, addr, count * 512);
    }

    /// **EVERY WRITE ANSWERED SO FAR, MADE DURABLE.** A SCSI disk is sent
    /// SYNCHRONIZE CACHE, unless it said it has no write cache. A virtio-blk
    /// disk writes through (see `write_cache`), so there is nothing to send;
    /// nor on a disk in memory, which counts the flush for host tests.
    /// Answers a virtio-blk status byte. `unflushed` is cleared only on
    /// success, so a failed flush is tried again before the next response.
    ///
    /// **WHAT TO DO IS `durable.step`'S** (a pure decision, driven by
    /// `durable_sim.zig`); only the SYNCHRONIZE CACHE it asks for is here.
    pub fn flush(self: *Block) u8 {
        var d = durable.Disk{ .unflushed = self.unflushed, .write_cache = self.write_cache, .asks = self.address != null };
        const s = durable.step(d);
        if (s == .none) return blk_s_ok;
        self.flushes +%= 1;
        // `synchronize` may learn the disk has no cache (ILLEGAL REQUEST),
        // and says so in `write_cache` and an ok.
        const status = if (s == .synchronize) scsi.synchronize(self, self.address.?) else blk_s_ok;
        if (durable.settle(&d, s, status == blk_s_ok)) {
            self.flush_failures +%= 1;
            // Reached under metal-vmm with VOLUME_SYNC_FAIL=1: on the host
            // every disk is in memory and writes through.
            props.reachable(@src(), "virtio: a flush fails, is counted, and the disk stays unflushed", .{ .failures = self.flush_failures });
        }
        self.unflushed = d.unflushed;
        return status;
    }
};

/// The memory a Block needs, which a caller places somewhere identity-mapped.
pub const BlockMemory = struct {
    ring: Block.Q.RingType align(16) = undefined,
    header: BlkReqHeader align(16) = undefined,
    status: u8 = 0,
    /// Used only when the Block is a disk on a SCSI controller.
    scsi: scsi.Memory = .{},

    pub fn bring(self: *BlockMemory, device: Device) Error!Block {
        return Block.init(device, self);
    }
};
