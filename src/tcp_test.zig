//! The TCP table's tests: a fake peer on the other end of a recording wire.
//! Every test drives the table with the frames a real client would send and
//! reads back the frames the table sent, checking the sequence numbers a real
//! client would check.

const std = @import("std");
const proto = @import("proto.zig");
const tcp = @import("tcp.zig");
const invariants = @import("tcp_check.zig");

const Conn = tcp.Conn;
const Table = tcp.Table;
const Result = tcp.Result;
const Event = tcp.Event;
const State = tcp.State;
const header_len = tcp.header_len;
const segment_at = tcp.segment_at;
const flag_fin = tcp.flag_fin;
const flag_syn = tcp.flag_syn;
const flag_rst = tcp.flag_rst;
const flag_psh = tcp.flag_psh;
const flag_ack = tcp.flag_ack;
const mss_option = tcp.mss_option;
const parseMss = tcp.parseMss;
const first_rto_ns = tcp.first_rto_ns;
const min_rto_ns = tcp.min_rto_ns;
const max_retries = tcp.max_retries;
const ns_per_ms = tcp.ns_per_ms;
const ns_per_s = tcp.ns_per_s;

const testing = std.testing;

const server_ip = [4]u8{ 10, 0, 2, 15 };
const server_mac = [6]u8{ 0x52, 0x54, 0, 0x12, 0x34, 0x56 };
const client_mac = [6]u8{ 0x52, 0x55, 0x0a, 0, 2, 2 };

/// The wire, recording what the table sent.
const Wire = struct {
    frames: [128][1600]u8 = undefined,
    lens: [128]usize = undefined,
    count: usize = 0,

    pub fn send(self: *Wire, frame: []const u8) void {
        @memcpy(self.frames[self.count][0..frame.len], frame);
        self.lens[self.count] = frame.len;
        self.count += 1;
    }

    const Seg = struct { dst_port: u16, seq: u32, ack: u32, flags: u8, window: u16, options: []const u8, payload: []const u8 };

    fn last(self: *Wire) Seg {
        return self.at(self.count - 1);
    }

    fn at(self: *Wire, k: usize) Seg {
        const pkt = proto.parseIpv4(self.frames[k][0..self.lens[k]]).?;
        const t = pkt.payload;
        const offset = @as(usize, t[12] >> 4) * 4;
        return .{
            .dst_port = proto.readBe16(t[2..4]),
            .seq = proto.readBe32(t[4..8]),
            .ack = proto.readBe32(t[8..12]),
            .flags = t[13],
            .window = proto.readBe16(t[14..16]),
            .options = t[header_len..offset],
            .payload = t[offset..],
        };
    }

    /// The segments sent since `from`, with their payload lengths.
    fn sizesSince(self: *Wire, from: usize, buf: []usize) []usize {
        for (from..self.count, 0..) |k, n| buf[n] = self.at(k).payload.len;
        return buf[0 .. self.count - from];
    }

    /// The window in the last segment, other than a reset, this machine
    /// sent to `ip`:`port`, or null if it has sent none.
    fn lastWindowTo(self: *Wire, ip: [4]u8, port: u16) ?u16 {
        var k = self.count;
        while (k > 0) {
            k -= 1;
            const pkt = proto.parseIpv4(self.frames[k][0..self.lens[k]]) orelse continue;
            if (!std.mem.eql(u8, &pkt.dst_ip, &ip)) continue;
            const s = self.at(k);
            if (s.dst_port != port or s.flags & flag_rst != 0) continue;
            return s.window;
        }
        return null;
    }

    /// The checksum a real peer would verify, recomputed.
    fn checksumOk(self: *Wire, k: usize) bool {
        const pkt = proto.parseIpv4(self.frames[k][0..self.lens[k]]).?;
        return proto.pseudoChecksum(pkt.src_ip, pkt.dst_ip, proto.proto_tcp, pkt.payload) == 0;
    }
};

/// **EVERY STEP A TEST TAKES IS CHECKED** (TCP_TESTING.md §1). Tests drive
/// the table through these two rather than its own methods, and after each
/// one every slot is held to tcp_check.zig's rules: the bookkeeping agrees
/// with itself, and everything a connection owes has a deadline that will
/// see it done. So every scenario below is also a test of every invariant,
/// at every step it takes.
///
/// A broken invariant is a bug in the table, not an outcome a test can
/// expect, so it stops the run with the rule, the connection and the time;
/// the stack trace names the step.
fn handle(table: *Table, wire: *Wire, frame: []const u8, now: i96) Result {
    const r = table.handle(wire, frame, now);
    verify(table, wire, now, .after_handle);
    return r;
}

fn transmit(table: *Table, wire: *Wire, now: i96) void {
    table.transmit(wire, now);
    verify(table, wire, now, .after_transmit);
}

fn verify(table: *Table, wire: *Wire, now: i96, phase: invariants.Phase) void {
    if (invariants.check(table, now, phase)) |v| {
        std.debug.panic("TCP invariant broken {s}, connection {d}, at {d} ns: {s} ({s})", .{
            @tagName(phase), v.conn, now, v.rule.says(), @tagName(v.rule),
        });
    }
    // What the table believes it told each peer is what the wire carried.
    for (table.conns, 0..) |*c, i| {
        if (c.state == .closed) continue;
        const said = wire.lastWindowTo(c.peer_ip, c.peer_port) orelse continue;
        if (said != c.told_wnd) {
            std.debug.panic("TCP invariant broken {s}, connection {d}, at {d} ns: told_wnd is {d}, but the last segment sent said {d}", .{
                @tagName(phase), i, now, c.told_wnd, said,
            });
        }
    }
}

/// **WHERE SEQUENCE NUMBERS START**, from build.zig: the suite runs once per
/// pair, near zero, half-way and the wrap (TCP_TESTING.md §6). Tests compare
/// sequence numbers only to each other, with `+%` and `-%`, never to a literal.
const start = @import("tcp_test_start");

/// The first initial sequence number each fixture hands out is `start.isn`,
/// and each after it 1000 further on.
var next_isn: u32 = 0;
fn fakeIsn() u32 {
    next_isn +%= 1000;
    return next_isn;
}

/// One client: an address, a port, and the sequence numbers it keeps.
const Peer = struct {
    ip: [4]u8,
    port: u16,
    seq: u32 = start.peer,
    ack: u32 = 0,
    window: u16 = 8192,
    /// The segment size its SYN says, if it says one.
    mss: ?u16 = null,

    fn frame(self: *const Peer, buf: []u8, flags: u8, seq: u32, data: []const u8) []const u8 {
        var options: [4]u8 = undefined;
        const olen: usize = if (flags & flag_syn != 0 and self.mss != null) 4 else 0;
        if (olen > 0) options = .{ 2, 4, @intCast(self.mss.? >> 8), @intCast(self.mss.? & 0xFF) };
        const len = header_len + olen;
        const frame_len = proto.writeIpv4(buf, client_mac, server_mac, self.ip, server_ip, proto.proto_tcp, len + data.len);
        const t = buf[segment_at..][0 .. len + data.len];
        @memcpy(t[0..2], &proto.be16(self.port));
        @memcpy(t[2..4], &proto.be16(80));
        @memcpy(t[4..8], &proto.be32(seq));
        @memcpy(t[8..12], &proto.be32(self.ack));
        t[12] = @intCast((len / 4) << 4);
        t[13] = flags;
        @memcpy(t[14..16], &proto.be16(self.window));
        @memcpy(t[16..20], &[_]u8{ 0, 0, 0, 0 });
        @memcpy(t[header_len..len], options[0..olen]);
        @memcpy(t[len..], data);
        @memcpy(t[16..18], &proto.be16(proto.pseudoChecksum(self.ip, server_ip, proto.proto_tcp, t)));
        return buf[0..frame_len];
    }

    /// SYN, then the ACK of the server's SYN-ACK. Returns the slot.
    fn connect(self: *Peer, table: *Table, wire: *Wire, now: i96) !usize {
        var buf: [1600]u8 = undefined;
        _ = handle(table, wire, self.frame(&buf, flag_syn, self.seq, ""), now);
        const synack = wire.last();
        try testing.expectEqual(flag_syn | flag_ack, synack.flags);
        try testing.expectEqual(self.seq +% 1, synack.ack);
        self.seq +%= 1;
        self.ack = synack.seq +% 1;
        const r = handle(table, wire, self.frame(&buf, flag_ack, self.seq, ""), now);
        try testing.expectEqual(Event.opened, r.event);
        return r.index;
    }

    fn write(self: *Peer, table: *Table, wire: *Wire, bytes: []const u8, now: i96) Result {
        var buf: [1600]u8 = undefined;
        const r = handle(table, wire, self.frame(&buf, flag_psh | flag_ack, self.seq, bytes), now);
        const reply = wire.last();
        self.seq = reply.ack; // what the server says it has
        return r;
    }

    fn fin(self: *Peer, table: *Table, wire: *Wire, now: i96) Result {
        var buf: [1600]u8 = undefined;
        return handle(table, wire, self.frame(&buf, flag_fin | flag_ack, self.seq, ""), now);
    }

    /// Acknowledges everything up to `number`, with the peer's window.
    fn ackUpTo(self: *Peer, table: *Table, wire: *Wire, number: u32, now: i96) Result {
        self.ack = number;
        var buf: [1600]u8 = undefined;
        return handle(table, wire, self.frame(&buf, flag_ack, self.seq, ""), now);
    }

    /// Acknowledges every byte the server has sent so far.
    fn ackAll(self: *Peer, table: *Table, wire: *Wire, now: i96) Result {
        const s = wire.last();
        const end = s.seq +% @as(u32, @intCast(s.payload.len)) +% @as(u32, if (s.flags & flag_fin != 0) 1 else 0);
        return self.ackUpTo(table, wire, end, now);
    }
};

const Fixture = struct {
    conns: [4]Conn = undefined,
    rx: [4][64]u8 = undefined,
    tx: [4][2048]u8 = undefined,
    out: [1600]u8 = undefined,
    wire: Wire = .{},
    table: Table = undefined,

    fn init(self: *Fixture) void {
        next_isn = start.isn -% 1000;
        for (&self.conns, &self.rx, &self.tx) |*c, *r, *t| c.* = .{ .rx = r, .tx = t };
        self.table = Table.init(server_ip, server_mac, 80, &self.conns, &self.out, fakeIsn);
    }
};

const ms: i96 = ns_per_ms;
/// The first wait before sending again, whatever it is set to.
const rto: i96 = first_rto_ns;

/// A payload whose every byte says where it is.
fn pattern(buf: []u8) []u8 {
    for (buf, 0..) |*b, k| b.* = @truncate(k *% 7 +% k / 256);
    return buf;
}

test "a handshake, a request, and the bytes are there to read" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    const r = p.write(&f.table, &f.wire, "GET / HTTP/1.1\r\n\r\n", 2);
    try testing.expectEqual(Event.data, r.event);
    try testing.expectEqual(i, r.index);
    try testing.expectEqualStrings("GET / HTTP/1.1\r\n\r\n", f.table.conns[i].pending());
    try testing.expect(f.wire.checksumOk(f.wire.count - 1));
    try testing.expectEqual(@as(i96, 1), f.table.conns[i].opened_at);
    try testing.expectEqual(@as(i96, 2), f.table.conns[i].heard_at);
}

test "two connections at once each keep their own bytes" {
    var f: Fixture = .{};
    f.init();
    var a = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var b = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40001 };
    const ia = try a.connect(&f.table, &f.wire, 1);
    const ib = try b.connect(&f.table, &f.wire, 2);
    try testing.expect(ia != ib);
    _ = a.write(&f.table, &f.wire, "from a ", 3);
    _ = b.write(&f.table, &f.wire, "from b", 4);
    _ = a.write(&f.table, &f.wire, "again", 5);
    try testing.expectEqualStrings("from a again", f.table.conns[ia].pending());
    try testing.expectEqualStrings("from b", f.table.conns[ib].pending());
    // Arrival order is kept, for serving the oldest first.
    try testing.expect(f.table.conns[ia].serial < f.table.conns[ib].serial);
}

test "the same port from another address is another connection" {
    var f: Fixture = .{};
    f.init();
    var a = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var b = Peer{ .ip = .{ 10, 0, 2, 3 }, .port = 40000 };
    const ia = try a.connect(&f.table, &f.wire, 1);
    const ib = try b.connect(&f.table, &f.wire, 1);
    try testing.expect(ia != ib);
}

test "a full table drops the SYN and counts it, and a freed slot is used again" {
    var f: Fixture = .{};
    f.init();
    var peers: [5]Peer = undefined;
    for (&peers, 0..) |*p, k| p.* = .{ .ip = .{ 10, 0, 2, 2 }, .port = @intCast(41000 + k) };
    var slots: [4]usize = undefined;
    for (peers[0..4], &slots) |*p, *s| s.* = try p.connect(&f.table, &f.wire, 1);

    var buf: [1600]u8 = undefined;
    const sent = f.wire.count;
    _ = handle(&f.table, &f.wire, peers[4].frame(&buf, flag_syn, peers[4].seq, ""), 2);
    try testing.expectEqual(sent, f.wire.count); // nothing answered
    try testing.expectEqual(@as(u64, 1), f.table.refused);

    f.table.abandon(&f.wire, slots[2]);
    const again = try peers[4].connect(&f.table, &f.wire, 3);
    try testing.expectEqual(slots[2], again);
}

test "a slot the host holds is not given to a stranger, even after it closes" {
    var f: Fixture = .{};
    f.init();
    var peers: [5]Peer = undefined;
    for (&peers, 0..) |*p, k| p.* = .{ .ip = .{ 10, 0, 2, 2 }, .port = @intCast(42000 + k) };
    var slots: [4]usize = undefined;
    for (peers[0..4], &slots) |*p, *s| s.* = try p.connect(&f.table, &f.wire, 1);

    f.table.claim(slots[0]);
    // The peer resets the connection the host is part-way through serving.
    var buf: [1600]u8 = undefined;
    const r = handle(&f.table, &f.wire, peers[0].frame(&buf, flag_rst, peers[0].seq, ""), 2);
    try testing.expectEqual(Event.closed, r.event);
    try testing.expectEqual(State.closed, f.table.conns[slots[0]].state);

    // A new SYN finds no free slot: the closed one is still the host's.
    _ = handle(&f.table, &f.wire, peers[4].frame(&buf, flag_syn, peers[4].seq, ""), 3);
    try testing.expectEqual(@as(u64, 1), f.table.refused);

    f.table.release(slots[0]);
    try testing.expectEqual(slots[0], try peers[4].connect(&f.table, &f.wire, 4));
}

test "the window is the room left, and reading gives it back" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    try testing.expectEqual(@as(u16, 64), f.wire.last().window);

    _ = p.write(&f.table, &f.wire, "0123456789" ** 4, 2);
    try testing.expectEqual(@as(u16, 24), f.wire.last().window);

    f.table.conns[i].consume(40);
    f.table.ack(&f.wire, i);
    try testing.expectEqual(@as(u16, 64), f.wire.last().window);
}

test "a window that reopens is said again until the peer sends" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = p.write(&f.table, &f.wire, "0123456789" ** 6, 1);
    try testing.expectEqual(@as(u16, 4), f.wire.last().window); // shut, as far as the peer can tell

    // The reader takes it all and says so; say that announcement is lost.
    f.table.conns[i].consume(60);
    f.table.ack(&f.wire, i);
    try testing.expectEqual(@as(u16, 64), f.wire.last().window);
    var from = f.wire.count;
    transmit(&f.table, &f.wire, 2); // starts the clock, sends nothing
    try testing.expectEqual(from, f.wire.count);
    transmit(&f.table, &f.wire, 2 + rto);
    try testing.expectEqual(from + 1, f.wire.count);
    try testing.expectEqual(flag_ack, f.wire.last().flags);
    try testing.expectEqual(@as(u16, 64), f.wire.last().window);
    try testing.expectEqual(@as(u64, 1), f.table.window_updates);

    // The peer sends: it saw the window, and nothing more is said.
    _ = p.write(&f.table, &f.wire, "more", 3 + rto);
    from = f.wire.count;
    transmit(&f.table, &f.wire, 60 * ns_per_s);
    try testing.expectEqual(from, f.wire.count);
}

test "a reader that makes room without saying so is announced for it" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = p.write(&f.table, &f.wire, "0123456789" ** 6, 1);
    f.table.conns[i].consume(60);
    const from = f.wire.count;
    transmit(&f.table, &f.wire, 2);
    try testing.expectEqual(from + 1, f.wire.count);
    try testing.expectEqual(@as(u16, 64), f.wire.last().window);
}

test "a peer with nothing more to say is told a bounded number of times" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = p.write(&f.table, &f.wire, "0123456789" ** 6, 1);
    f.table.conns[i].consume(60);
    f.table.ack(&f.wire, i);
    var now: i96 = 2;
    while (now < 120 * ns_per_s) : (now += 10 * ms) transmit(&f.table, &f.wire, now);
    try testing.expectEqual(@as(u64, max_retries), f.table.window_updates);
    try testing.expectEqual(State.established, f.table.conns[i].state);
}

test "a segment bigger than the room is taken in part, and the rest arrives after a read" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    const body = "abcdefghij" ** 10; // 100 bytes into a 64-byte buffer

    var buf: [1600]u8 = undefined;
    const first = p.seq;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_psh | flag_ack, first, body), 2);
    try testing.expectEqual(first +% 64, f.wire.last().ack); // only what fitted
    try testing.expectEqualStrings(body[0..64], f.table.conns[i].pending());

    // The peer sends the rest again from where the ACK said.
    f.table.conns[i].consume(64);
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_psh | flag_ack, first +% 64, body[64..]), 3);
    try testing.expectEqual(first +% 100, f.wire.last().ack);
    try testing.expectEqualStrings(body[64..], f.table.conns[i].pending());
}

test "consuming part of a request compacts the buffer once the front is half gone" {
    var b: [16]u8 = undefined;
    var c = Conn{ .rx = &b, .tx = &.{} };
    @memcpy(b[0..12], "abcdefghijkl");
    c.end = 12;
    c.consume(3);
    try testing.expectEqual(@as(usize, 3), c.start); // not yet past half
    try testing.expectEqualStrings("defghijkl", c.pending());
    c.consume(6);
    try testing.expectEqual(@as(usize, 0), c.start); // compacted
    try testing.expectEqualStrings("jkl", c.pending());
    try testing.expectEqual(@as(usize, 13), c.room());
    c.consume(3);
    try testing.expectEqual(@as(usize, 16), c.room());
    c.consume(5); // more than there is is just all of it
    try testing.expectEqual(@as(usize, 0), c.pending().len);
}

test "a segment out of order is dropped and the right one asked for again" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_psh | flag_ack, p.seq +% 5, "later"), 2);
    try testing.expectEqual(p.seq, f.wire.last().ack);
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].pending().len);
}

test "the peer's FIN leaves what it sent to be read, and we can still answer" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    _ = p.write(&f.table, &f.wire, "GET / HTTP/1.1\r\n\r\n", 2);
    const r = p.fin(&f.table, &f.wire, 3);
    try testing.expectEqual(Event.peer_done, r.event);
    try testing.expect(f.table.conns[i].peer_done);
    try testing.expectEqualStrings("GET / HTTP/1.1\r\n\r\n", f.table.conns[i].pending());
    try testing.expectEqual(p.seq +% 1, f.wire.last().ack); // their FIN acknowledged
    try testing.expectEqual(@as(usize, 19), f.table.queue(i, "HTTP/1.1 200 OK\r\n\r\n"));
    transmit(&f.table, &f.wire, 4);
    try testing.expectEqualStrings("HTTP/1.1 200 OK\r\n\r\n", f.wire.last().payload);
}

test "our FIN, then theirs acknowledging it, closes it and frees the slot" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    f.table.finish(i);
    transmit(&f.table, &f.wire, 2);
    try testing.expectEqual(flag_fin | flag_ack, f.wire.last().flags);
    try testing.expectEqual(State.closing, f.table.conns[i].state);
    p.ack = f.wire.last().seq +% 1;
    const r = p.fin(&f.table, &f.wire, 3);
    try testing.expectEqual(Event.closed, r.event);
    try testing.expectEqual(p.seq +% 1, f.wire.last().ack); // their FIN answered
    try testing.expectEqual(State.closed, f.table.conns[i].state);
    try testing.expectEqual(@as(usize, 0), f.table.inUse());
}

test "our FIN acknowledged first: the connection waits for theirs, acknowledges it, and closes" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    f.table.finish(i);
    transmit(&f.table, &f.wire, 2);
    try testing.expectEqual(Event.nothing, p.ackAll(&f.table, &f.wire, 3).event);
    try testing.expectEqual(State.closing, f.table.conns[i].state);
    try testing.expectEqual(tcp.Fin.acknowledged, f.table.conns[i].fin);
    // Nothing more of ours goes out while it waits.
    const sent = f.wire.count;
    transmit(&f.table, &f.wire, 3 + 10 * rto);
    try testing.expectEqual(sent, f.wire.count);

    const r = p.fin(&f.table, &f.wire, 4);
    try testing.expectEqual(Event.closed, r.event);
    try testing.expectEqual(flag_ack, f.wire.last().flags);
    try testing.expectEqual(p.seq +% 1, f.wire.last().ack); // their FIN acknowledged
    try testing.expectEqual(p.ack, f.wire.last().seq);
    try testing.expectEqual(@as(usize, 0), f.table.inUse());
}

test "a peer that never sends its FIN is let go after the wait" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    f.table.finish(i);
    transmit(&f.table, &f.wire, 0);
    _ = p.ackAll(&f.table, &f.wire, 1);
    transmit(&f.table, &f.wire, 1 + tcp.fin_wait_ns - 1);
    try testing.expectEqual(State.closing, f.table.conns[i].state);
    transmit(&f.table, &f.wire, 1 + tcp.fin_wait_ns);
    try testing.expectEqual(State.closed, f.table.conns[i].state);
    try testing.expectEqual(flag_rst | flag_ack, f.wire.last().flags);
    try testing.expectEqual(@as(u64, 1), f.table.fin_waits_expired);
}

test "their FIN repeated before we close is acknowledged again, and after, reset" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    try testing.expectEqual(Event.peer_done, p.fin(&f.table, &f.wire, 1).event);
    const first_ack = f.wire.last();
    // Our acknowledgement was lost; they say it again.
    const sent = f.wire.count;
    try testing.expectEqual(Event.nothing, p.fin(&f.table, &f.wire, 2).event);
    try testing.expectEqual(sent + 1, f.wire.count);
    try testing.expectEqual(first_ack.ack, f.wire.last().ack);

    f.table.finish(i);
    transmit(&f.table, &f.wire, 3);
    p.seq +%= 1;
    try testing.expectEqual(Event.closed, p.ackAll(&f.table, &f.wire, 4).event);
    // Closed and forgotten: their FIN once more is refused.
    p.seq -%= 1;
    _ = p.fin(&f.table, &f.wire, 5);
    try testing.expectEqual(flag_rst, f.wire.last().flags);
    try testing.expectEqual(p.ack, f.wire.last().seq);
}

test "their FIN before our FIN is acknowledged leaves the connection closing" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    f.table.finish(i);
    transmit(&f.table, &f.wire, 2);
    const r = p.fin(&f.table, &f.wire, 3); // acknowledges only the SYN
    try testing.expectEqual(Event.peer_done, r.event);
    try testing.expectEqual(State.closing, f.table.conns[i].state);
    p.seq +%= 1;
    try testing.expectEqual(Event.closed, p.ackUpTo(&f.table, &f.wire, p.ack +% 1, 4).event);
}

test "a segment for no connection is answered with a reset; a stray reset is not" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .ack = 777 };
    var buf: [1600]u8 = undefined;
    // With an acknowledgement: the reset is numbered by it.
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, p.frame(&buf, flag_ack, 1, "hello"), 1).event);
    try testing.expectEqual(@as(usize, 1), f.wire.count);
    try testing.expectEqual(flag_rst, f.wire.last().flags);
    try testing.expectEqual(@as(u32, 777), f.wire.last().seq);
    try testing.expectEqual(@as(u16, 40000), f.wire.last().dst_port);
    try testing.expect(f.wire.checksumOk(0));
    // Without one: the reset acknowledges what the segment carried, its FIN too.
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_fin, 50, "abc"), 1);
    try testing.expectEqual(flag_rst | flag_ack, f.wire.last().flags);
    try testing.expectEqual(@as(u32, 54), f.wire.last().ack);
    // A reset is never answered.
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, p.frame(&buf, flag_rst, 1, ""), 1).event);
    try testing.expectEqual(@as(usize, 2), f.wire.count);
    try testing.expectEqual(@as(u64, 2), f.table.strays);
    try testing.expectEqual(@as(usize, 0), f.table.inUse());
}

test "a repeated SYN is answered again, from the same starting number" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 1);
    const first = f.wire.last();
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 2);
    const second = f.wire.last();
    try testing.expectEqual(first.seq, second.seq);
    try testing.expectEqual(flag_syn | flag_ack, second.flags);
    try testing.expectEqual(@as(usize, 1), f.table.inUse()); // not a second connection
}

test "every initial sequence number comes from the supplied source" {
    var f: Fixture = .{};
    f.init();
    var a = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var b = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40001 };
    _ = try a.connect(&f.table, &f.wire, 1);
    const first = a.ack -% 1;
    _ = try b.connect(&f.table, &f.wire, 1);
    const second = b.ack -% 1;
    try testing.expectEqual(first +% 1000, second);
}

test "a frame that is not ours changes nothing" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    const frame = p.frame(&buf, flag_syn, 1, "");
    // Another port.
    var other: [1600]u8 = undefined;
    @memcpy(other[0..frame.len], frame);
    @memcpy(other[segment_at + 2 .. segment_at + 4], &proto.be16(8080));
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, other[0..frame.len], 1).event);
    // Garbage.
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, "not a frame", 1).event);
    try testing.expectEqual(@as(usize, 0), f.table.inUse());
}

// ── the send side ────────────────────────────────────────────────────────────

test "the SYN-ACK says our segment size, and the peer's sizes what we send" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .mss = 100 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 1);
    try testing.expectEqualSlices(u8, &mss_option, f.wire.last().options);
    try testing.expect(f.wire.checksumOk(f.wire.count - 1));
    p.seq +%= 1;
    p.ack = f.wire.last().seq +% 1;
    const i = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq, ""), 1).index;

    var body: [250]u8 = undefined;
    try testing.expectEqual(@as(usize, 250), f.table.queue(i, pattern(&body)));
    const from = f.wire.count;
    transmit(&f.table, &f.wire, 2);
    var sizes: [8]usize = undefined;
    try testing.expectEqualSlices(usize, &.{ 100, 100, 50 }, f.wire.sizesSince(from, &sizes));
    try testing.expectEqualSlices(u8, body[200..], f.wire.last().payload);
    try testing.expectEqual(p.ack +% 200, f.wire.last().seq);
}

test "a peer that names no segment size is sent 536 bytes at a time" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    var body: [1200]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    const from = f.wire.count;
    transmit(&f.table, &f.wire, 2);
    var sizes: [8]usize = undefined;
    try testing.expectEqualSlices(usize, &.{ 536, 536, 128 }, f.wire.sizesSince(from, &sizes));
}

test "only what the window allows goes out, and an acknowledgement lets more go" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 100 };
    const i = try p.connect(&f.table, &f.wire, 1);
    var body: [250]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));

    var from = f.wire.count;
    transmit(&f.table, &f.wire, 2);
    var sizes: [8]usize = undefined;
    try testing.expectEqualSlices(usize, &.{100}, f.wire.sizesSince(from, &sizes));
    from = f.wire.count;
    transmit(&f.table, &f.wire, 3);
    try testing.expectEqual(from, f.wire.count); // the window is full

    // Forty bytes acknowledged: forty more may go.
    _ = p.ackUpTo(&f.table, &f.wire, p.ack +% 40, 4);
    transmit(&f.table, &f.wire, 5);
    try testing.expectEqualSlices(usize, &.{40}, f.wire.sizesSince(from, &sizes));
    try testing.expectEqualSlices(u8, body[100..140], f.wire.last().payload);
    try testing.expectEqual(@as(usize, 210), f.table.conns[i].queued());
}

test "an acknowledgement of nothing sent, or an old one, changes nothing" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    const first = p.ack;
    _ = f.table.queue(i, "hello");
    transmit(&f.table, &f.wire, 2);

    p.window = 0;
    _ = p.ackUpTo(&f.table, &f.wire, first +% 6, 3); // one past what was sent
    try testing.expectEqual(@as(usize, 5), f.table.conns[i].queued());
    try testing.expectEqual(@as(u32, 8192), f.table.conns[i].wnd);
    _ = p.ackUpTo(&f.table, &f.wire, first -% 1, 3); // before the start
    try testing.expectEqual(@as(usize, 5), f.table.conns[i].queued());
    try testing.expectEqual(@as(u32, 8192), f.table.conns[i].wnd);

    _ = p.ackUpTo(&f.table, &f.wire, first +% 5, 4);
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].queued());
    try testing.expectEqual(@as(?i96, null), f.table.conns[i].rto_at);
}

test "what is not acknowledged in time is sent again, oldest first, and the wait doubles" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    var body: [700]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 0); // 536 + 164
    _ = p.ackUpTo(&f.table, &f.wire, p.ack +% 100, 50 * ms); // the timer restarts from here

    var from = f.wire.count;
    transmit(&f.table, &f.wire, 50 * ms + rto - 1);
    try testing.expectEqual(from, f.wire.count); // not yet
    transmit(&f.table, &f.wire, 50 * ms + rto);
    var sizes: [8]usize = undefined;
    try testing.expectEqualSlices(usize, &.{ 536, 64 }, f.wire.sizesSince(from, &sizes));
    try testing.expectEqualSlices(u8, body[100..636], f.wire.at(from).payload);
    try testing.expectEqual(p.ack, f.wire.at(from).seq);
    try testing.expectEqual(@as(u64, 1), f.table.retransmits);

    from = f.wire.count;
    transmit(&f.table, &f.wire, 50 * ms + 3 * rto - 1);
    try testing.expectEqual(from, f.wire.count); // the wait is twice as long now
    transmit(&f.table, &f.wire, 50 * ms + 3 * rto);
    try testing.expectEqual(from + 2, f.wire.count);

    // Progress resets the wait.
    _ = p.ackUpTo(&f.table, &f.wire, p.ack +% 600, 100 * ms + 3 * rto);
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].queued());
    try testing.expectEqual(@as(u8, 0), f.table.conns[i].retries);
    try testing.expectEqual(first_rto_ns, f.table.conns[i].rto_ns);
}

test "a peer that never answers is reset after the last timeout" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = f.table.queue(i, "anyone there?");
    var now: i96 = 0;
    transmit(&f.table, &f.wire, now);
    var sends: usize = 1;
    while (f.table.conns[i].state != .closed) {
        now += 1 * ms;
        const before = f.wire.count;
        transmit(&f.table, &f.wire, now);
        if (f.wire.count > before and f.table.conns[i].state != .closed) sends += 1;
        try testing.expect(now < 120 * ns_per_s);
    }
    try testing.expectEqual(@as(usize, 1 + max_retries), sends);
    try testing.expectEqual(flag_rst | flag_ack, f.wire.last().flags);
    try testing.expectEqual(@as(u64, 1), f.table.given_up);
    // Each wait doubles up to the ceiling, and the last one runs out too.
    var total: i96 = 0;
    var wait: i96 = rto;
    for (0..max_retries + 1) |_| {
        total += wait;
        wait = @min(wait * 2, tcp.max_rto_ns);
    }
    try testing.expectEqual(total, now);
}

test "a shut window is probed, and the rest goes when it opens" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 0 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = f.table.queue(i, "0123456789");
    var from = f.wire.count;
    transmit(&f.table, &f.wire, 0);
    try testing.expectEqual(from, f.wire.count); // no room
    transmit(&f.table, &f.wire, rto);
    try testing.expectEqualStrings("0", f.wire.last().payload); // the probe
    try testing.expectEqual(@as(u64, 1), f.table.probes);

    // The peer took the byte and has room now.
    p.window = 100;
    _ = p.ackUpTo(&f.table, &f.wire, p.ack +% 1, rto + 10 * ms);
    from = f.wire.count;
    transmit(&f.table, &f.wire, rto + 10 * ms);
    try testing.expectEqualStrings("123456789", f.wire.last().payload);
    try testing.expectEqual(from + 1, f.wire.count);
}

test "a window that stays shut is given up on" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 0 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = f.table.queue(i, "nobody is reading");
    var now: i96 = 0;
    while (f.table.conns[i].state != .closed) : (now += 10 * ms) {
        transmit(&f.table, &f.wire, now);
        // The peer answers every probe, still with no room.
        if (f.wire.count > 0 and f.wire.last().payload.len > 0) _ = p.ackUpTo(&f.table, &f.wire, p.ack, now);
        try testing.expect(now < 60 * ns_per_s);
    }
    try testing.expectEqual(@as(u64, 1), f.table.given_up);
}

test "our FIN waits for the queue, and only its own acknowledgement closes" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 100 };
    const i = try p.connect(&f.table, &f.wire, 0);
    var body: [150]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    f.table.finish(i);
    try testing.expectEqual(@as(usize, 0), f.table.queue(i, "too late"));
    transmit(&f.table, &f.wire, 1);
    try testing.expectEqual(flag_psh | flag_ack, f.wire.last().flags); // no FIN yet

    // Every byte so far acknowledged is not the FIN acknowledged.
    try testing.expectEqual(Event.nothing, p.ackAll(&f.table, &f.wire, 2).event);
    try testing.expectEqual(State.closing, f.table.conns[i].state);
    transmit(&f.table, &f.wire, 3);
    try testing.expectEqual(@as(usize, 50), f.wire.at(f.wire.count - 2).payload.len);
    try testing.expectEqual(flag_fin | flag_ack, f.wire.last().flags);
    try testing.expectEqual(p.ack +% 50, f.wire.last().seq);
    try testing.expectEqual(Event.nothing, p.ackUpTo(&f.table, &f.wire, p.ack +% 50, 4).event);
    try testing.expectEqual(State.closing, f.table.conns[i].state);
    try testing.expectEqual(tcp.Fin.sent, f.table.conns[i].fin);
    try testing.expectEqual(Event.nothing, p.ackUpTo(&f.table, &f.wire, p.ack +% 1, 5).event);
    try testing.expectEqual(tcp.Fin.acknowledged, f.table.conns[i].fin);
    try testing.expectEqual(Event.closed, p.fin(&f.table, &f.wire, 6).event);
}

test "a lost FIN is sent again" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    f.table.finish(i);
    transmit(&f.table, &f.wire, 0);
    const fin = f.wire.last();
    const before = f.wire.count;
    transmit(&f.table, &f.wire, rto);
    // A segment of its own: the last one on the wire being the old FIN proves
    // nothing (tools/mutate_tcp.py's go-back-keeps-fin passed that way).
    try testing.expectEqual(before + 1, f.wire.count);
    try testing.expectEqual(flag_fin | flag_ack, f.wire.last().flags);
    try testing.expectEqual(fin.seq, f.wire.last().seq);
    _ = p.ackAll(&f.table, &f.wire, rto + 10 * ms);
    try testing.expectEqual(Event.closed, p.fin(&f.table, &f.wire, rto + 20 * ms).event);
}

test "a lost SYN-ACK is sent again by the timer" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    const first = f.wire.last();
    transmit(&f.table, &f.wire, rto - 1);
    try testing.expectEqual(@as(usize, 1), f.wire.count);
    transmit(&f.table, &f.wire, rto);
    try testing.expectEqual(first.seq, f.wire.last().seq);
    try testing.expectEqual(flag_syn | flag_ack, f.wire.last().flags);
}

test "an ACK with the wrong number does not complete the handshake" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    const r = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    _ = r;
    p.seq +%= 1;
    p.ack = f.wire.last().seq +% 2;
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq, ""), 0).event);
    try testing.expectEqual(State.syn_received, f.table.conns[0].state);
}

test "a full queue takes only what fits, and acknowledged bytes make room" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    var body: [3000]u8 = undefined;
    _ = pattern(&body);
    try testing.expectEqual(@as(usize, 2048), f.table.queue(i, &body));
    try testing.expectEqual(@as(usize, 0), f.table.queue(i, body[2048..]));
    transmit(&f.table, &f.wire, 0);
    _ = p.ackUpTo(&f.table, &f.wire, p.ack +% 1000, 1);
    try testing.expectEqual(@as(usize, 952), f.table.queue(i, body[2048..]));
    try testing.expectEqual(@as(usize, 2000), f.table.conns[i].queued());

    // What goes out next is still the stream in order: all 2048 of the first
    // queueing were on the wire, and the rest follows them.
    _ = p.ackAll(&f.table, &f.wire, 2);
    try testing.expectEqual(@as(usize, 952), f.table.conns[i].queued());
    var sent: usize = 2048;
    while (f.table.conns[i].queued() > 0) {
        const from = f.wire.count;
        transmit(&f.table, &f.wire, 3);
        for (from..f.wire.count) |k| {
            const s = f.wire.at(k);
            try testing.expectEqualSlices(u8, body[sent..][0..s.payload.len], s.payload);
            sent += s.payload.len;
        }
        _ = p.ackAll(&f.table, &f.wire, 3);
        f.wire.count = 0;
    }
    try testing.expectEqual(@as(usize, 3000), sent);
}

test "nothing is queued before the handshake completes" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    try testing.expectEqual(@as(usize, 0), f.table.queue(0, "early"));
}

test "abandoning a connection tells the peer" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    f.table.abandon(&f.wire, i);
    try testing.expectEqual(flag_rst | flag_ack, f.wire.last().flags);
    try testing.expectEqual(p.ack, f.wire.last().seq);
    try testing.expectEqual(State.closed, f.table.conns[i].state);
}

test "segment-size options are read past padding and other options" {
    try testing.expectEqual(@as(?u16, 1460), parseMss(&.{ 1, 1, 4, 2, 2, 4, 5, 180 }));
    try testing.expectEqual(@as(?u16, null), parseMss(&.{ 0, 2, 4, 5, 180 }));
    try testing.expectEqual(@as(?u16, null), parseMss(&.{ 2, 4, 5 }));
    try testing.expectEqual(@as(?u16, null), parseMss(&.{ 3, 0 }));
    try testing.expectEqual(@as(?u16, null), parseMss(&.{}));
}

// ── what the RFC review found ────────────────────────────────────────────────

test "a keepalive probe is acknowledged, and does not count as hearing from the peer" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    var buf: [1600]u8 = undefined;
    const sent = f.wire.count;
    // A keepalive: one before the next byte expected, and no data.
    const r = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq -% 1, ""), 50);
    try testing.expectEqual(Event.nothing, r.event);
    try testing.expectEqual(sent + 1, f.wire.count);
    try testing.expectEqual(flag_ack, f.wire.last().flags);
    try testing.expectEqual(p.seq, f.wire.last().ack);
    try testing.expectEqual(@as(i96, 1), f.table.conns[i].heard_at);
}

test "a reset at the window's right edge is outside it, and is ignored" {
    // RFC 9293: in the window means RCV.NXT <= SEG.SEQ < RCV.NXT + RCV.WND,
    // so the edge itself is outside, and RFC 5961 drops a reset there.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    const c = &f.table.conns[i];
    const edge = c.rcv_nxt +% @as(u32, c.window());
    const before = f.wire.count;
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_rst | flag_ack, edge, ""), 1);
    try testing.expectEqual(before, f.wire.count);
    try testing.expectEqual(State.established, c.state);
    // One byte inside it draws the challenge.
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_rst | flag_ack, edge -% 1, ""), 2);
    try testing.expectEqual(before + 1, f.wire.count);
}

test "a reset counts only at the next byte expected; one inside the window draws an acknowledgement" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 1);
    var buf: [1600]u8 = undefined;
    var sent = f.wire.count;
    // Far outside the window: ignored, without a word.
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, p.frame(&buf, flag_rst, p.seq +% 100_000, ""), 2).event);
    try testing.expectEqual(sent, f.wire.count);
    // Inside it but not exact: a challenge.
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, p.frame(&buf, flag_rst, p.seq +% 10, ""), 2).event);
    try testing.expectEqual(sent + 1, f.wire.count);
    try testing.expectEqual(p.seq, f.wire.last().ack);
    try testing.expectEqual(State.established, f.table.conns[i].state);
    // Exact: the connection is over.
    sent = f.wire.count;
    try testing.expectEqual(Event.closed, handle(&f.table, &f.wire, p.frame(&buf, flag_rst, p.seq, ""), 3).event);
    try testing.expectEqual(sent, f.wire.count);
}

test "a late acknowledgement does not overrule a newer one's window" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 4000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    var body: [300]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 1);
    const first = p.ack;
    _ = p.ackUpTo(&f.table, &f.wire, first +% 100, 2); // newer, window 4000
    p.window = 0;
    _ = p.ackUpTo(&f.table, &f.wire, first, 3); // older, window 0, arriving late
    try testing.expectEqual(@as(u32, 4000), f.table.conns[i].wnd);
}

test "an acknowledgement of bytes sent before a timeout still counts after it" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 2000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    var body: [1000]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 0);
    const first = p.ack;
    // The window shuts, the timer runs out, and one byte goes as a probe.
    p.window = 0;
    _ = p.ackUpTo(&f.table, &f.wire, first, 1);
    transmit(&f.table, &f.wire, rto);
    try testing.expectEqual(@as(usize, 1), f.table.conns[i].sent);
    // The peer's acknowledgement of all 1000 bytes is still good.
    p.window = 2000;
    _ = p.ackUpTo(&f.table, &f.wire, first +% 1000, rto + 1);
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].queued());
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].sent);
    try testing.expectEqual(first +% 1000, f.table.conns[i].una);
    try testing.expectEqual(@as(?i96, null), f.table.conns[i].rto_at);
}

test "a handshake ACK of something we never sent is refused with a reset" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    p.seq +%= 1;
    p.ack = f.wire.last().seq +% 99;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq, ""), 0);
    try testing.expectEqual(flag_rst, f.wire.last().flags);
    try testing.expectEqual(p.ack, f.wire.last().seq);
    try testing.expectEqual(State.syn_received, f.table.conns[0].state);
}

test "a segment with a wrong checksum is dropped" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    var buf: [1600]u8 = undefined;
    const frame = p.frame(&buf, flag_psh | flag_ack, p.seq, "hello");
    buf[frame.len - 1] ^= 0xFF; // damage the payload
    const sent = f.wire.count;
    try testing.expectEqual(Event.nothing, handle(&f.table, &f.wire, frame, 1).event);
    try testing.expectEqual(sent, f.wire.count);
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].pending().len);
    try testing.expectEqual(@as(u64, 1), f.table.damaged);
}

test "a SYN on an established connection draws an acknowledgement, not a new connection" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    _ = try p.connect(&f.table, &f.wire, 0);
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, 99, ""), 1);
    try testing.expectEqual(flag_ack, f.wire.last().flags);
    try testing.expectEqual(@as(usize, 1), f.table.inUse());
}

test "data ahead of what is expected still carries its acknowledgement" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = f.table.queue(i, "answer");
    transmit(&f.table, &f.wire, 0);
    p.ack +%= 6;
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_psh | flag_ack, p.seq +% 5, "later"), 1);
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].queued()); // "answer" acknowledged
    try testing.expectEqual(@as(usize, 0), f.table.conns[i].pending().len); // "later" not taken
    try testing.expectEqual(p.seq, f.wire.last().ack);
}

test "their FIN first, then again with ours acknowledged, closes at once" {
    // Seen from QEMU's network: the repeated FIN is numbered before what we
    // now expect, and its acknowledgement of our FIN must still count.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    try testing.expectEqual(Event.peer_done, p.fin(&f.table, &f.wire, 1).event);
    f.table.finish(i);
    transmit(&f.table, &f.wire, 2);
    try testing.expectEqual(flag_fin | flag_ack, f.wire.last().flags);
    p.ack = f.wire.last().seq +% 1;
    // The same FIN again, numbered as before, now acknowledging ours.
    try testing.expectEqual(Event.closed, p.fin(&f.table, &f.wire, 3).event);
    try testing.expectEqual(State.closed, f.table.conns[i].state);
}

test "what we send after going back is still numbered at the furthest we sent" {
    // **SND.NXT IS NOT THE RETRANSMISSION POINTER.** A timeout rewinds what to
    // send next; it does not un-send anything. A peer whose window shut can
    // only be probed one byte at a time, so the rewound pointer stays near
    // `una` — and a reset numbered there is behind the peer's window, which
    // discards it and leaves the peer waiting forever on a connection we have
    // already thrown away.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    var body: [100]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 1);
    try testing.expectEqual(@as(usize, 100), f.wire.last().payload.len);
    const una = f.table.conns[i].una;

    // The peer stops reading: it acknowledges nothing new and shuts its window.
    p.window = 0;
    _ = p.ackUpTo(&f.table, &f.wire, una, 2);

    var t: i96 = 2;
    var k: usize = 0;
    while (k <= max_retries) : (k += 1) {
        t += 6 * ns_per_s;
        transmit(&f.table, &f.wire, t);
    }
    try testing.expect(f.table.given_up == 1);
    try testing.expectEqual(State.closed, f.table.conns[i].state);
    const rst = f.wire.last();
    try testing.expectEqual(flag_rst | flag_ack, rst.flags);
    try testing.expectEqual(una +% 100, rst.seq); // where the peer's window begins
}

test "a segment beyond the window carries nothing, not even its acknowledgement" {
    // Taking the acknowledgement of a segment the peer cannot have sent yet
    // would set SND.WL1 past anything it will ever send, and then the window
    // rule refuses every real update that follows.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = f.table.queue(i, "ten bytes!");
    transmit(&f.table, &f.wire, 1);
    const una = f.table.conns[i].una;
    const wl1 = f.table.conns[i].wl1;

    p.ack = una +% 10; // it claims to have the lot
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq +% 100_000, ""), 2);

    try testing.expectEqual(una, f.table.conns[i].una);
    try testing.expectEqual(wl1, f.table.conns[i].wl1);
    try testing.expectEqual(@as(usize, 10), f.table.conns[i].queued());
    try testing.expectEqual(flag_ack, f.wire.last().flags);
}

test "an older segment with a newer acknowledgement moves the window's edge too" {
    // The window rule keeps the old segment from setting the window, but its
    // acknowledgement still moves `una` — and the window is measured from
    // `una`, so the edge has to come back by as much.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = p.write(&f.table, &f.wire, "GET /\r\n\r\n", 1);
    var body: [50]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 2);
    const before = f.table.conns[i].wnd;

    p.ack = f.table.conns[i].una +% 50;
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq -% 20, ""), 3);

    try testing.expectEqual(@as(usize, 0), f.table.conns[i].queued());
    try testing.expectEqual(before - 50, f.table.conns[i].wnd);
}

test "giving up on a handshake tells the peer, instead of leaving it on a timer" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    const synack = f.wire.last();
    try testing.expectEqual(flag_syn | flag_ack, synack.flags);

    var t: i96 = 0;
    var k: usize = 0;
    while (k <= max_retries) : (k += 1) {
        t += 6 * ns_per_s;
        transmit(&f.table, &f.wire, t);
    }
    const rst = f.wire.last();
    try testing.expect(rst.flags & flag_rst != 0);
    try testing.expectEqual(synack.seq +% 1, rst.seq);
    try testing.expectEqual(State.closed, f.table.conns[0].state);
    try testing.expect(f.table.given_up == 1);
}

test "the handshake is the first measurement, and the estimate is RFC 6298's" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    const synack = f.wire.last();
    p.seq +%= 1;
    p.ack = synack.seq +% 1;
    // Their acknowledgement comes back 100 ms later: that is the round trip.
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq, ""), 100 * ms);
    const c = &f.table.conns[0];
    try testing.expectEqual(@as(u64, 100 * ns_per_ms), c.srtt_ns);
    try testing.expectEqual(@as(u64, 50 * ns_per_ms), c.rttvar_ns); // half, for the first
    try testing.expectEqual(@as(u64, 300 * ns_per_ms), c.rto_ns); // srtt + 4 * rttvar

    // A second sample, 60 ms: the estimate moves an eighth, the variation a
    // quarter.
    _ = f.table.queue(0, "a response");
    transmit(&f.table, &f.wire, 200 * ms);
    _ = p.ackAll(&f.table, &f.wire, 260 * ms);
    try testing.expectEqual(@as(u64, 95 * ns_per_ms), c.srtt_ns);
    try testing.expectEqual(@as(u64, 47_500_000), c.rttvar_ns);
    try testing.expectEqual(@as(u64, 285 * ns_per_ms), c.rto_ns);
}

test "a fast path waits the floor, not a second" {
    // **THE FLOOR IS THE PEER'S DELAYED ACKNOWLEDGEMENTS, NOT THE PATH.** On a
    // private network the round trip is microseconds; nothing should wait a
    // second for it.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    p.seq +%= 1;
    p.ack = f.wire.last().seq +% 1;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq, ""), 200_000); // 0.2 ms
    try testing.expectEqual(@as(u64, 200_000), f.table.conns[0].srtt_ns);
    try testing.expectEqual(min_rto_ns, f.table.conns[0].rto_ns);
}

test "a segment that was sent twice is not timed" {
    // Karn's algorithm: there is no telling which copy the acknowledgement
    // answers, so the sample would be wrong either way.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 0);
    p.seq +%= 1;
    p.ack = f.wire.last().seq +% 1;
    _ = handle(&f.table, &f.wire, p.frame(&buf, flag_ack, p.seq, ""), 100 * ms);
    const c = &f.table.conns[0];
    const settled = c.srtt_ns;

    _ = f.table.queue(0, "a response");
    transmit(&f.table, &f.wire, 200 * ms);
    // Nothing comes back, so it goes again — and then the acknowledgement
    // arrives a long time after the FIRST copy went out.
    transmit(&f.table, &f.wire, 200 * ms + @as(i96, @intCast(c.rto_ns)));
    try testing.expectEqual(@as(u64, 1), f.table.retransmits);
    _ = p.ackAll(&f.table, &f.wire, 900 * ms);
    try testing.expectEqual(settled, c.srtt_ns);
}

test "three duplicate acknowledgements send it again at once" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 8192 };
    const i = try p.connect(&f.table, &f.wire, ms);
    var body: [3000]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 2 * ms);
    const una = f.table.conns[i].una;
    const sent = f.wire.count;

    // The peer received what came after the first segment, and says so three
    // times. It is nowhere near the retransmission timer.
    for (0..3) |_| _ = p.ackUpTo(&f.table, &f.wire, una, 3 * ms);
    try testing.expectEqual(@as(u64, 1), f.table.fast_retransmits);
    try testing.expect(f.wire.count > sent);
    try testing.expectEqual(una, f.wire.at(sent).seq); // from the oldest byte on
    try testing.expectEqual(@as(u8, 0), f.table.conns[i].dupacks);
}

test "a fast retransmit restarts the timer, so the timeout does not follow on its heels" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 8192 };
    const i = try p.connect(&f.table, &f.wire, ms);
    var body: [3000]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 2 * ms); // the timer: 2 ms + rto
    const una = f.table.conns[i].una;
    for (0..3) |_| _ = p.ackUpTo(&f.table, &f.wire, una, 3 * ms);
    try testing.expectEqual(@as(u64, 1), f.table.retransmits);
    // When the first timer would have run out, the restarted one has not.
    transmit(&f.table, &f.wire, 2 * ms + rto);
    try testing.expectEqual(@as(u64, 1), f.table.retransmits);
    transmit(&f.table, &f.wire, 3 * ms + rto);
    try testing.expectEqual(@as(u64, 2), f.table.retransmits);
}

test "duplicates before new data are forgotten: a new run needs three of its own" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 8192 };
    const i = try p.connect(&f.table, &f.wire, ms);
    var body: [3000]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 2 * ms);
    const una = f.table.conns[i].una;
    // Two duplicates, then something new acknowledged, then two more: never
    // three in a row, so nothing is sent again early.
    for (0..2) |_| _ = p.ackUpTo(&f.table, &f.wire, una, 3 * ms);
    _ = p.ackUpTo(&f.table, &f.wire, una +% 100, 3 * ms);
    for (0..2) |_| _ = p.ackUpTo(&f.table, &f.wire, una +% 100, 3 * ms);
    try testing.expectEqual(@as(u64, 0), f.table.fast_retransmits);
    _ = p.ackUpTo(&f.table, &f.wire, una +% 100, 3 * ms);
    try testing.expectEqual(@as(u64, 1), f.table.fast_retransmits);
}

test "a peer that repeats itself forever gets one answer, not one each time" {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 8192 };
    const i = try p.connect(&f.table, &f.wire, ms);
    var body: [3000]u8 = undefined;
    _ = f.table.queue(i, pattern(&body));
    transmit(&f.table, &f.wire, 2 * ms);
    const una = f.table.conns[i].una;
    for (0..30) |_| _ = p.ackUpTo(&f.table, &f.wire, una, 3 * ms);
    try testing.expectEqual(@as(u64, 1), f.table.fast_retransmits);
}

test "a shut window's probes are answered without being mistaken for loss" {
    // The peer has no room, so it acknowledges every probe with the same
    // number: the same shape as a duplicate acknowledgement, and not loss.
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000, .window = 0 };
    const i = try p.connect(&f.table, &f.wire, 0);
    _ = f.table.queue(i, "nobody is reading");
    var now: i96 = 0;
    while (f.table.conns[i].state != .closed) : (now += 10 * ms) {
        transmit(&f.table, &f.wire, now);
        if (f.wire.count > 0 and f.wire.last().payload.len > 0) _ = p.ackUpTo(&f.table, &f.wire, p.ack, now);
    }
    try testing.expectEqual(@as(u64, 0), f.table.fast_retransmits);
    try testing.expectEqual(@as(u64, 1), f.table.given_up);
}

// ── the state × event matrix (TCP_TESTING.md §7) ───────────────────────────
//
// RFC 9293 §3.10.7 says, state by state, what each kind of segment does. Here
// that is a table: one row per kind of segment, one cell per state, each cell
// what must be sent back and what the connection becomes. A null cell is a
// question about the design rather than a test, and is listed under §7 in
// TCP_TESTING.md; it stays null until the question has an answer.

/// Where the connection is when the segment arrives. `listen` is no
/// connection at all: a segment from a peer the table does not know, to the
/// port it listens on. `fin_queued`, `fin_sent` and `fin_acked` are RFC 9293's
/// FIN-WAIT-1 before and after our FIN goes, and FIN-WAIT-2; `peer_done` is
/// CLOSE-WAIT.
const MatrixSetup = enum { listen, syn_received, established, fin_queued, fin_sent, fin_acked, peer_done };

/// What arrives. In the comments, `r` is RCV.NXT, `u` SND.UNA and `h` SND.NXT
/// (`highest()`). Every segment but the SYNs and the `ack_*` rows carries the
/// acknowledgement a well-behaved peer would send now: `h` in a handshake
/// (completing it), `u` otherwise (acknowledging nothing new).
const MatrixKind = enum {
    /// SYN at r-1: the peer's SYN again.
    syn_repeated,
    /// SYN at r.
    syn_in_window,
    /// SYN at r, carrying 5 bytes.
    syn_with_data,
    /// RST at r.
    rst_exact,
    /// RST at r+1.
    rst_in_window,
    /// RST at r+1000, past the 64-byte window.
    rst_outside,
    /// ACK of u-10.
    ack_old,
    /// ACK of u.
    ack_duplicate,
    /// ACK of h.
    ack_new,
    /// ACK of h+10: of something never sent.
    ack_future,
    /// 10 bytes at r.
    data_in_order,
    /// 10 bytes at r+5.
    data_ahead,
    /// 10 bytes at r-20: all of them already taken.
    data_behind,
    /// 10 bytes at r-5: five old, five new.
    data_straddling,
    /// 10 bytes at r+1000.
    data_past_window,
    /// FIN at r — in `peer_done`, the peer's FIN repeated, at r-1.
    fin,
    /// 10 bytes and a FIN at r.
    fin_with_data,
};

const MatrixReply = enum { none, ack, syn_ack, rst };

const MatrixOutcome = struct {
    reply: MatrixReply,
    /// The state after, or null for "as it was": state, our FIN and the
    /// peer's FIN all unchanged.
    state: ?State = null,
    fin: ?tcp.Fin = null,
    peer_done: ?bool = null,
    /// Bytes the segment added to what waits to be read (not checked once
    /// the slot is closed).
    taken: usize = 0,
    event: ?Event = null,
};

const MatrixRow = struct { kind: MatrixKind, cells: [7]?MatrixOutcome };

const cell_none: ?MatrixOutcome = .{ .reply = .none };
const cell_ack: ?MatrixOutcome = .{ .reply = .ack };
const cell_rst: ?MatrixOutcome = .{ .reply = .rst };
/// To a peer the table does not know: refused with a reset, nothing opened.
const cell_refused: ?MatrixOutcome = .{ .reply = .rst, .state = .closed };
/// Ignored without a word, and nothing opened (or the slot closed).
const cell_quiet_closed: ?MatrixOutcome = .{ .reply = .none, .state = .closed };
/// A handshake begun: a SYN-ACK that acknowledges the SYN and nothing else.
const cell_opens: ?MatrixOutcome = .{ .reply = .syn_ack, .state = .syn_received };

const matrix_rows = [_]MatrixRow{
    //                                   listen        syn_received       established fin_queued fin_sent  fin_acked peer_done
    // A SYN-ACK said again, for a peer whose copy was lost (as Linux does;
    // RFC 9293's first check would send a bare ACK, which a client still
    // waiting for our SYN cannot use).
    .{ .kind = .syn_repeated, .cells = .{ cell_opens, .{ .reply = .syn_ack }, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack } },
    // RFC 5961 §4: a SYN on a synchronized connection gets a challenge ACK.
    .{ .kind = .syn_in_window, .cells = .{ cell_opens, null, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack } },
    .{ .kind = .syn_with_data, .cells = .{ cell_opens, null, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack } },
    // RFC 5961 §3: only an exact reset resets; one in the window is
    // challenged; anything else is dropped.
    .{ .kind = .rst_exact, .cells = .{ cell_quiet_closed, cell_quiet_closed, cell_quiet_closed, cell_quiet_closed, cell_quiet_closed, cell_quiet_closed, cell_quiet_closed } },
    .{ .kind = .rst_in_window, .cells = .{ cell_quiet_closed, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack } },
    .{ .kind = .rst_outside, .cells = .{ cell_quiet_closed, cell_none, cell_none, cell_none, cell_none, cell_none, cell_none } },
    // In a handshake an ACK of anything but our SYN is refused with a reset
    // numbered by it; on a synchronized connection an old or duplicate one
    // is ignored.
    .{ .kind = .ack_old, .cells = .{ cell_refused, cell_rst, cell_none, cell_none, cell_none, cell_none, cell_none } },
    .{ .kind = .ack_duplicate, .cells = .{ cell_refused, cell_rst, cell_none, cell_none, cell_none, cell_none, cell_none } },
    .{
        .kind = .ack_new,
        .cells = .{
            cell_refused,
            .{ .reply = .none, .state = .established, .event = .opened },
            cell_none, // nothing in flight: the same as a duplicate
            cell_none,
            .{ .reply = .none, .state = .closing, .fin = .acknowledged },
            cell_none,
            cell_none,
        },
    },
    .{ .kind = .ack_future, .cells = .{ cell_refused, cell_rst, null, null, null, null, null } },
    // Text is taken in ESTABLISHED and both FIN-WAITs, and ignored (but
    // acknowledged) once the peer has sent its FIN.
    .{ .kind = .data_in_order, .cells = .{
        cell_refused,
        .{ .reply = .ack, .state = .established, .taken = 10, .event = .opened },
        .{ .reply = .ack, .taken = 10, .event = .data },
        .{ .reply = .ack, .taken = 10 },
        .{ .reply = .ack, .taken = 10 },
        .{ .reply = .ack, .taken = 10 },
        cell_ack,
    } },
    // In-order only: anything else is acknowledged and dropped.
    .{ .kind = .data_ahead, .cells = .{ cell_refused, null, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack } },
    .{ .kind = .data_behind, .cells = .{ cell_refused, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack } },
    .{ .kind = .data_straddling, .cells = .{ cell_refused, null, null, null, null, null, cell_ack } },
    .{ .kind = .data_past_window, .cells = .{ cell_refused, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack, cell_ack } },
    // The peer's FIN: CLOSE-WAIT from a handshake or ESTABLISHED; CLOSING
    // from FIN-WAIT-1; and from FIN-WAIT-2 closed at once, there being no
    // TIME-WAIT here.
    .{ .kind = .fin, .cells = .{
        cell_refused,
        .{ .reply = .ack, .state = .established, .peer_done = true, .event = .peer_done },
        .{ .reply = .ack, .state = .established, .peer_done = true, .event = .peer_done },
        .{ .reply = .ack, .state = .closing, .fin = .queued, .peer_done = true, .event = .peer_done },
        .{ .reply = .ack, .state = .closing, .fin = .sent, .peer_done = true, .event = .peer_done },
        .{ .reply = .ack, .state = .closed, .event = .closed },
        cell_ack,
    } },
    .{ .kind = .fin_with_data, .cells = .{
        cell_refused,
        .{ .reply = .ack, .state = .established, .peer_done = true, .taken = 10 },
        .{ .reply = .ack, .state = .established, .peer_done = true, .taken = 10, .event = .peer_done },
        .{ .reply = .ack, .state = .closing, .fin = .queued, .peer_done = true, .taken = 10 },
        .{ .reply = .ack, .state = .closing, .fin = .sent, .peer_done = true, .taken = 10 },
        .{ .reply = .ack, .state = .closed, .event = .closed },
        cell_ack,
    } },
};

test "the state × event matrix: every state, every kind of segment" {
    for (matrix_rows) |row| {
        for (std.enums.values(MatrixSetup), row.cells) |setup, cell| {
            const want = cell orelse continue; // a question: TCP_TESTING.md §7
            matrixCell(setup, row.kind, want) catch |err| {
                std.debug.print("the matrix cell for {s} in {s} failed\n", .{ @tagName(row.kind), @tagName(setup) });
                return err;
            };
        }
    }
}

/// Builds a fresh table in `setup`, sends one segment of `kind`, and holds the
/// result to `want`.
fn matrixCell(setup: MatrixSetup, kind: MatrixKind, want: MatrixOutcome) !void {
    var f: Fixture = .{};
    f.init();
    var p = Peer{ .ip = .{ 10, 0, 2, 2 }, .port = 40000 };
    var buf: [1600]u8 = undefined;
    // A fresh table gives its first connection the first slot.
    const i: usize = 0;
    switch (setup) {
        .listen => {},
        .syn_received => {
            _ = handle(&f.table, &f.wire, p.frame(&buf, flag_syn, p.seq, ""), 1);
            p.seq +%= 1;
            p.ack = f.wire.last().seq +% 1;
        },
        .established, .fin_queued, .fin_sent, .fin_acked, .peer_done => {
            try testing.expectEqual(i, try p.connect(&f.table, &f.wire, 1));
            switch (setup) {
                .fin_queued => f.table.finish(i),
                .fin_sent, .fin_acked => {
                    f.table.finish(i);
                    transmit(&f.table, &f.wire, 1);
                    try testing.expectEqual(flag_fin | flag_ack, f.wire.last().flags);
                    if (setup == .fin_acked) _ = p.ackAll(&f.table, &f.wire, 1);
                },
                .peer_done => _ = p.fin(&f.table, &f.wire, 1),
                else => {},
            }
        },
    }

    const c = &f.table.conns[i];
    const known = setup != .listen;
    const r: u32 = if (known) c.rcv_nxt else p.seq;
    const u: u32 = if (known) c.una else 0x1234_5678;
    const h: u32 = if (known) c.highest() else u;
    const now_ack: u32 = if (setup == .syn_received) h else u;
    const was_state = c.state;
    const was_fin = c.fin;
    const was_peer_done = c.peer_done;
    const had = c.pending().len;
    const from = f.wire.count;

    const got = handle(&f.table, &f.wire, matrixSegment(kind, setup, &p, &buf, r, u, h, now_ack), 2);

    switch (want.reply) {
        .none => try testing.expectEqual(from, f.wire.count),
        .ack, .syn_ack, .rst => {
            try testing.expect(f.wire.count > from);
            const said = f.wire.last();
            switch (want.reply) {
                .ack => try testing.expectEqual(flag_ack, said.flags),
                .syn_ack => try testing.expectEqual(flag_syn | flag_ack, said.flags),
                .rst => try testing.expect(said.flags & flag_rst != 0),
                .none => unreachable,
            }
            if (want.reply == .rst) {
                // Numbered by the acknowledgement it answers.
                try testing.expectEqual(p.ack, said.seq);
            } else if (c.state != .closed) {
                try testing.expectEqual(c.rcv_nxt, said.ack);
                const numbered = if (want.reply == .syn_ack) c.una else c.highest();
                try testing.expectEqual(numbered, said.seq);
            }
        },
    }

    if (want.state) |state| {
        try testing.expectEqual(state, c.state);
        if (want.fin) |fin_now| try testing.expectEqual(fin_now, c.fin);
        if (want.peer_done) |done| try testing.expectEqual(done, c.peer_done);
    } else {
        try testing.expectEqual(was_state, c.state);
        try testing.expectEqual(was_fin, c.fin);
        try testing.expectEqual(was_peer_done, c.peer_done);
    }
    if (c.state != .closed) try testing.expectEqual(had + want.taken, c.pending().len);
    if (want.event) |event| try testing.expectEqual(event, got.event);
}

/// The segment of `kind`, from `p`, numbered from the connection's `r`, `u`
/// and `h` (see `MatrixKind`), acknowledging `now_ack` unless the kind says
/// otherwise.
fn matrixSegment(kind: MatrixKind, setup: MatrixSetup, p: *Peer, buf: []u8, r: u32, u: u32, h: u32, now_ack: u32) []const u8 {
    const ten = "0123456789";
    const data_flags = flag_psh | flag_ack;
    p.ack = now_ack;
    switch (kind) {
        .ack_old => p.ack = u -% 10,
        .ack_duplicate => p.ack = u,
        .ack_new => p.ack = h,
        .ack_future => p.ack = h +% 10,
        else => {},
    }
    return switch (kind) {
        .syn_repeated => p.frame(buf, flag_syn, r -% 1, ""),
        .syn_in_window => p.frame(buf, flag_syn, r, ""),
        .syn_with_data => p.frame(buf, flag_syn, r, "hello"),
        .rst_exact => p.frame(buf, flag_rst | flag_ack, r, ""),
        .rst_in_window => p.frame(buf, flag_rst | flag_ack, r +% 1, ""),
        .rst_outside => p.frame(buf, flag_rst | flag_ack, r +% 1000, ""),
        .ack_old, .ack_duplicate, .ack_new, .ack_future => p.frame(buf, flag_ack, r, ""),
        .data_in_order => p.frame(buf, data_flags, r, ten),
        .data_ahead => p.frame(buf, data_flags, r +% 5, ten),
        .data_behind => p.frame(buf, data_flags, r -% 20, ten),
        .data_straddling => p.frame(buf, data_flags, r -% 5, ten),
        .data_past_window => p.frame(buf, data_flags, r +% 1000, ten),
        .fin => p.frame(buf, flag_fin | flag_ack, if (setup == .peer_done) r -% 1 else r, ""),
        .fin_with_data => p.frame(buf, flag_fin | data_flags, r, ten),
    };
}
